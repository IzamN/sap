#Область ПрограммныйИнтерфейс 

Процедура РассылкаПоказателейПЗК(Дата, СлужебноеЗадание = Неопределено) Экспорт
	
	Если СлужебноеЗадание = Неопределено Тогда 
		РезультатЗапроса = ПолучитьВыборкуПоВсем(Дата);
	Иначе
		РезультатЗапроса = ПолучитьВыборкуПоСлужебномуЗаданию(СлужебноеЗадание, Дата);
	КонецЕсли; 
	
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат;
	КонецЕсли;
	
	ВыборкаОбщая = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаОбщая.Следующий() Цикл
		
		Выборка = ВыборкаОбщая.Выбрать();
		
		Структура = ПолучитьТабДокСПараметрами(Выборка, Дата); 
		
		Если Структура = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		ТабДок = Структура.ТабличныйДокумент;
		Параметры = Структура.Параметры;
		
		ПотокФайла = Новый ПотокВПамяти();
		ТабДок.Записать(ПотокФайла, ТипФайлаТабличногоДокумента.PDF);
		
		ДвоичныеДанныеФайла = ПотокФайла.ЗакрытьИПолучитьДвоичныеДанные();
		
		АдресХранилищаДокумента = Новый ХранилищеЗначения(ДвоичныеДанныеФайла, Новый СжатиеДанных());
		
		//Отправка по указанной почте табличного документа
		Попытка				
			
			ПараметрыПисьма = ПолучитьПараметрыПисьма(Параметры.АдресЭП, Параметры.Фамилия,
			Параметры.Имя, Параметры.Отчество,
			Параметры.Период, АдресХранилищаДокумента);
			
			ЗаписьВОчередь = РегистрыСведений.ЭП_ОчередьОтправкиПочтовыхСообщений.СоздатьМенеджерЗаписи();
			ЗаписьВОчередь.Период = ТекущаяДатаСеанса();
			ЗаписьВОчередь.Пользователь = Параметры.Пользователь;
			ЗаписьВОчередь.ПараметрыПисьма = Новый ХранилищеЗначения(ПараметрыПисьма, Новый СжатиеДанных());
			ЗаписьВОчередь.Записать();
			
			Запись = РегистрыСведений.ЭП_РассылкаСлужебныхЗаданий.СоздатьМенеджерЗаписи();
			Запись.Месяц = НачалоМесяца(Дата);
			Запись.Сотрудник = Параметры.Сотрудник;
			Запись.ДатаОтправки = ЗаписьВОчередь.Период;
			Запись.Записать();
			
		Исключение
			
			Описание = ОписаниеОшибки();									
			Информация = ИнформацияОбОшибке();					
			Подробности = ПодробноеПредставлениеОшибки(Информация);
			ЗаписьЖурналаРегистрации("Рассылка показателей ПЗК", УровеньЖурналаРегистрации.Ошибка, , , 
			СтрШаблон("Не удалось добавить запись сотрудника: %1 - %2", ВыборкаОбщая.Сотрудник, Подробности));
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры  

Функция ПолучитьТабДокДляСлужебногоЗадания(Дата, СлужебноеЗадание) Экспорт
	
	РезультатЗапроса = ПолучитьВыборкуПоСлужебномуЗаданию(СлужебноеЗадание, Дата); 
	ВыборкаСГруппой = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если ВыборкаСГруппой.Следующий() тогда 
		Возврат ПолучитьТабДокСПараметрами(ВыборкаСГруппой.Выбрать(), Дата).ТабличныйДокумент;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПроцедурыИФункции    

Функция ПолучитьТабДокСПараметрами(Выборка, Дата)
	
	Если Выборка.Следующий() Тогда 
		
		Параметры = Новый Структура;
		Параметры.Вставить("АдресЭП", Выборка.АдресЭП);
		Параметры.Вставить("Фамилия", Выборка.Фамилия);
		Параметры.Вставить("Имя", Выборка.Имя);
		Параметры.Вставить("Отчество", Выборка.Отчество);
		Параметры.Вставить("Пользователь", Выборка.Пользователь);
		Параметры.Вставить("ИННОрганизации", Выборка.ИННОрганизации);
		Параметры.Вставить("Сотрудник", Выборка.Сотрудник);
		Параметры.Вставить("Период", Формат(Дата, "ДФ='MMMM yyyy'"));
		
		Данные = Новый Структура;
		Данные.Вставить("ФИО", СтрШаблон("%1 %2 %3", Параметры.Фамилия,
			Параметры.Имя, Параметры.Отчество));
		Данные.Вставить("Должность", Выборка.Должность); 
		Данные.Вставить("Период", Параметры.Период);  
		Данные.Вставить("Руководитель", Выборка.ФИОРуководителя);
		Данные.Вставить("ДолжностьРуководителя", Выборка.ДолжностьРуководителя);
		
		ТабДок = Новый ТабличныйДокумент;
		Макет = ПолучитьОбщийМакет("ЭП_ПФ_ПоказателиСотрудника"); 
		
		//Вывод верхнего колонтитула 
		Если Параметры.ИННОрганизации = "2310207111  " Тогда 
			ТабДок.Вывести(Макет.ПолучитьОбласть("ВерхнийКолонтитулЭдитПро"));
		ИначеЕсли  Параметры.ИННОрганизации = "9726012757  " Тогда 
			ТабДок.Вывести(Макет.ПолучитьОбласть("ВерхнийКолонтитулЦЦТ"));
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		//Вывод шапка
		Область = Макет.ПолучитьОбласть("Шапка");
		ЗаполнитьЗначенияСвойств(Область.Параметры, Данные);
		ТабДок.Вывести(Область);
		
		//Вывод шапки таблицы заданий 
		Если Выборка.Задание <> Null Тогда 
			ТабДок.Вывести(Макет.ПолучитьОбласть("ШапкаТаблицыЗадания"));
			Индекс = 1;
			Область = Макет.ПолучитьОбласть("СтрокаТаблицыЗадания");
			Область.Параметры.Н = Индекс;
			Область.Параметры.Задание = Выборка.Задание;
			ТабДок.Вывести(Область);
			Пока Выборка.Следующий() И Выборка.Задание <> Null Цикл
				Индекс = Индекс + 1;
				Область.Параметры.Н = Индекс;
				Область.Параметры.Задание = Выборка.Задание;
				ТабДок.Вывести(Область);
			КонецЦикла;
		КонецЕсли;
		
		Если Выборка.Показатели <> Неопределено И Выборка.Показатели <> Null Тогда
			
			//Вывод шапки таблицы показатели
			ТабДок.Вывести(Макет.ПолучитьОбласть("ШапкаТаблицыПоказатели"));
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицыПоказатели");
			Индекс = 1;
			Область.Параметры.Н = Индекс;
			Область.Параметры.Показатель = Выборка.Показатели;
			ТабДок.Вывести(Область); 
			Пока Выборка.Следующий() Цикл
				Индекс = Индекс + 1;
				Область.Параметры.Н = Индекс;
				Область.Параметры.Показатель = Выборка.Показатели;
				ТабДок.Вывести(Область); 
			КонецЦикла;  
			
		КонецЕсли;
		
		//Вывод подвала
		Область = Макет.ПолучитьОбласть("Подвал");
		ЗаполнитьЗначенияСвойств(Область.Параметры, Данные);
		ТабДок.Вывести(Область);
		
		ТабДок.ТолькоПросмотр = Истина;
		ТабДок.ОтображатьСетку = Ложь;
		ТабДок.РазмерСтраницы = "A4";
		
		Возврат Новый Структура("ТабличныйДокумент, Параметры", ТабДок, Параметры);
		
	Иначе 
		
		Возврат Неопределено; 
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьВыборкуПоСлужебномуЗаданию(СлужебноеЗадание, Дата)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(ЭП_Сотрудники.Фамилия + "" "", """") + ЕСТЬNULL(ЭП_Сотрудники.Имя + "" "", """") + ЕСТЬNULL(ЭП_Сотрудники.Отчество + "" "", """") КАК ФИОРуководителя,
	               |	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЭП_Сотрудники.Должность) КАК ДолжностьРуководителя,
	               |	Подразделения.Ссылка КАК Департамент
	               |ПОМЕСТИТЬ ВтДепартаментыРуководители
	               |ИЗ
	               |	Справочник.Подразделения КАК Подразделения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭП_Сотрудники КАК ЭП_Сотрудники
	               |			ПО Пользователи.ЭП_Сотрудник = ЭП_Сотрудники.Ссылка
	               |		ПО Подразделения.Руководитель = Пользователи.Ссылка
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Департамент
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СлужебноеЗадание.Сотрудник КАК Сотрудник,
	               |	Задания.Задание КАК Задание
	               |ПОМЕСТИТЬ ВтСлужебныеЗадания
	               |ИЗ
	               |	Документ.ЭП_СлужебноеЗадание КАК СлужебноеЗадание
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭП_СлужебноеЗадание.СлужебныеЗадания КАК Задания
	               |		ПО СлужебноеЗадание.Ссылка = Задания.Ссылка
	               |ГДЕ
	               |	СлужебноеЗадание.Ссылка = &СлужебноеЗадание
	               |	И НЕ СлужебноеЗадание.ПометкаУдаления
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Сотрудник
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЭП_Сотрудники.Ссылка КАК Сотрудник,
	               |	ЭП_Сотрудники.Фамилия КАК Фамилия,
	               |	ЭП_Сотрудники.Имя КАК Имя,
	               |	ЭП_Сотрудники.Отчество КАК Отчество,
	               |	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЭП_Сотрудники.Должность) КАК Должность,
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЭП_СотрудникиРасчетныеПрофили.Профиль) КАК КоличествоПрофилей,
	               |	Пользователи.Ссылка КАК Пользователь,
	               |	ВтДепартаментыРуководители.ФИОРуководителя КАК ФИОРуководителя,
	               |	ВтДепартаментыРуководители.ДолжностьРуководителя КАК ДолжностьРуководителя,
	               |	МАКСИМУМ(ПользователиКонтактнаяИнформация.АдресЭП) КАК АдресЭП,
	               |	ЭП_Организации.ИНН КАК ИННОрганизации,
	               |	ЭП_Сотрудники.НеУчаствуетВПЗК КАК НеУчаствуетВПЗК
	               |ПОМЕСТИТЬ ВтСотрудники
	               |ИЗ
	               |	Справочник.ЭП_Сотрудники КАК ЭП_Сотрудники
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭП_Сотрудники.РасчетныеПрофили КАК ЭП_СотрудникиРасчетныеПрофили
	               |		ПО (ЭП_СотрудникиРасчетныеПрофили.Ссылка = ЭП_Сотрудники.Ссылка)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
	               |			ПО (ПользователиКонтактнаяИнформация.Ссылка = Пользователи.Ссылка)
	               |		ПО (Пользователи.ЭП_Сотрудник = ЭП_Сотрудники.Ссылка)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВтДепартаментыРуководители КАК ВтДепартаментыРуководители
	               |		ПО ЭП_Сотрудники.Департамент = ВтДепартаментыРуководители.Департамент
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭП_Организации КАК ЭП_Организации
	               |		ПО ЭП_Сотрудники.Организация = ЭП_Организации.Ссылка
	               |ГДЕ
	               |	НЕ ЭП_Сотрудники.ПометкаУдаления
	               |	И НЕ ЭП_Сотрудники.ДоговорГПХ
	               |	И ЭП_Сотрудники.СтатусСотрудника <> ЗНАЧЕНИЕ(Перечисление.ЭП_СтатусыСотрудников.Уволен)
	               |	И ПользователиКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
	               |	И ЭП_Сотрудники.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ВтСлужебныеЗадания.Сотрудник
	               |			ИЗ
	               |				ВтСлужебныеЗадания)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЭП_Сотрудники.Ссылка,
	               |	ЭП_Сотрудники.Фамилия,
	               |	ЭП_Сотрудники.Имя,
	               |	ЭП_Сотрудники.Отчество,
	               |	Пользователи.Ссылка,
	               |	ВтДепартаментыРуководители.ФИОРуководителя,
	               |	ВтДепартаментыРуководители.ДолжностьРуководителя,
	               |	ЭП_Организации.ИНН,
	               |	ЭП_Сотрудники.НеУчаствуетВПЗК,
	               |	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЭП_Сотрудники.Должность)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Сотрудник
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВтСотрудники.Сотрудник КАК Сотрудник,
	               |	ВтСотрудники.Фамилия КАК Фамилия,
	               |	ВтСотрудники.Имя КАК Имя,
	               |	ВтСотрудники.Отчество КАК Отчество,
	               |	ВтСотрудники.Должность КАК Должность,
	               |	ЭП_СотрудникиРасчетныеПрофили.Профиль КАК Профиль,
	               |	ВтСотрудники.Пользователь КАК Пользователь,
	               |	ВтСотрудники.ФИОРуководителя КАК ФИОРуководителя,
	               |	ВтСотрудники.ДолжностьРуководителя КАК ДолжностьРуководителя,
	               |	ВтСотрудники.АдресЭП КАК АдресЭП,
	               |	ВтСотрудники.ИННОрганизации КАК ИННОрганизации
	               |ПОМЕСТИТЬ ВтСотрудникиСПрофилем
	               |ИЗ
	               |	ВтСотрудники КАК ВтСотрудники
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭП_Сотрудники.РасчетныеПрофили КАК ЭП_СотрудникиРасчетныеПрофили
	               |		ПО ВтСотрудники.Сотрудник = ЭП_СотрудникиРасчетныеПрофили.Ссылка
	               |ГДЕ
	               |	НЕ ВтСотрудники.НеУчаствуетВПЗК
	               |	И ВтСотрудники.КоличествоПрофилей = 1
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВтСотрудникиСПрофилем.Сотрудник КАК Сотрудник,
	               |	ВтСотрудникиСПрофилем.Фамилия КАК Фамилия,
	               |	ВтСотрудникиСПрофилем.Имя КАК Имя,
	               |	ВтСотрудникиСПрофилем.Отчество КАК Отчество,
	               |	ВтСотрудникиСПрофилем.Должность КАК Должность,
	               |	ВтСотрудникиСПрофилем.Пользователь КАК Пользователь,
	               |	ВЫБОР
	               |		КОГДА ДЛИНАСТРОКИ(ЭП_ПрофилиРасчетаРезультативностиПоказатели.Показатель.НаименованиеПолное) = 0
	               |			ТОГДА ЭП_ПрофилиРасчетаРезультативностиПоказатели.Показатель.Наименование
	               |		ИНАЧЕ ЭП_ПрофилиРасчетаРезультативностиПоказатели.Показатель.НаименованиеПолное
	               |	КОНЕЦ КАК Показатели,
	               |	ВтСотрудникиСПрофилем.ФИОРуководителя КАК ФИОРуководителя,
	               |	ВтСотрудникиСПрофилем.ДолжностьРуководителя КАК ДолжностьРуководителя,
	               |	ВтСотрудникиСПрофилем.АдресЭП КАК АдресЭП,
	               |	ВтСотрудникиСПрофилем.ИННОрганизации КАК ИННОрганизации,
	               |	NULL КАК Задание
	               |ИЗ
	               |	ВтСотрудникиСПрофилем КАК ВтСотрудникиСПрофилем
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭП_ПрофилиРасчетаРезультативности.Показатели КАК ЭП_ПрофилиРасчетаРезультативностиПоказатели
	               |		ПО ВтСотрудникиСПрофилем.Профиль = ЭП_ПрофилиРасчетаРезультативностиПоказатели.Ссылка
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВтСотрудники.Сотрудник,
	               |	ВтСотрудники.Фамилия,
	               |	ВтСотрудники.Имя,
	               |	ВтСотрудники.Отчество,
	               |	ВтСотрудники.Должность,
	               |	ВтСотрудники.Пользователь,
	               |	NULL,
	               |	ВтСотрудники.ФИОРуководителя,
	               |	ВтСотрудники.ДолжностьРуководителя,
	               |	ВтСотрудники.АдресЭП,
	               |	ВтСотрудники.ИННОрганизации,
	               |	ВтСлужебныеЗадания.Задание
	               |ИЗ
	               |	ВтСлужебныеЗадания КАК ВтСлужебныеЗадания
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСотрудники КАК ВтСотрудники
	               |		ПО ВтСлужебныеЗадания.Сотрудник = ВтСотрудники.Сотрудник
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Задание УБЫВ
	               |ИТОГИ ПО
	               |	Сотрудник";	
	
	Запрос.УстановитьПараметр("СлужебноеЗадание", СлужебноеЗадание); 
	Возврат Запрос.Выполнить();

КонецФункции

Функция ПолучитьВыборкуПоВсем(Дата)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(ЭП_Сотрудники.Фамилия + "" "", """") + ЕСТЬNULL(ЭП_Сотрудники.Имя + "" "", """") + ЕСТЬNULL(ЭП_Сотрудники.Отчество + "" "", """") КАК ФИОРуководителя,
	               |	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЭП_Сотрудники.Должность) КАК ДолжностьРуководителя,
	               |	Подразделения.Ссылка КАК Департамент
	               |ПОМЕСТИТЬ ВтДепартаментыРуководители
	               |ИЗ
	               |	Справочник.Подразделения КАК Подразделения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭП_Сотрудники КАК ЭП_Сотрудники
	               |			ПО Пользователи.ЭП_Сотрудник = ЭП_Сотрудники.Ссылка
	               |		ПО Подразделения.Руководитель = Пользователи.Ссылка
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Департамент
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЭП_СлужебноеЗадание.Сотрудник КАК Сотрудник,
	               |	ЭП_СлужебноеЗаданиеСлужебныеЗадания.Задание КАК Задание
	               |ПОМЕСТИТЬ ВтСлужебныеЗадания
	               |ИЗ
	               |	Документ.ЭП_СлужебноеЗадание.СлужебныеЗадания КАК ЭП_СлужебноеЗаданиеСлужебныеЗадания
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭП_СлужебноеЗадание КАК ЭП_СлужебноеЗадание
	               |		ПО ЭП_СлужебноеЗаданиеСлужебныеЗадания.Ссылка = ЭП_СлужебноеЗадание.Ссылка
	               |ГДЕ
	               |	НЕ ЭП_СлужебноеЗадание.ПометкаУдаления
	               |	И ЭП_СлужебноеЗадание.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Сотрудник
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЭП_Сотрудники.Ссылка КАК Сотрудник,
	               |	ЭП_Сотрудники.Фамилия КАК Фамилия,
	               |	ЭП_Сотрудники.Имя КАК Имя,
	               |	ЭП_Сотрудники.Отчество КАК Отчество,
	               |	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЭП_Сотрудники.Должность) КАК Должность,
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЭП_СотрудникиРасчетныеПрофили.Профиль) КАК КоличествоПрофилей,
	               |	Пользователи.Ссылка КАК Пользователь,
	               |	ВтДепартаментыРуководители.ФИОРуководителя КАК ФИОРуководителя,
	               |	ВтДепартаментыРуководители.ДолжностьРуководителя КАК ДолжностьРуководителя,
	               |	МАКСИМУМ(ПользователиКонтактнаяИнформация.АдресЭП) КАК АдресЭП,
	               |	ЭП_Организации.ИНН КАК ИННОрганизации,
	               |	ЭП_Сотрудники.НеУчаствуетВПЗК КАК НеУчаствуетВПЗК
	               |ПОМЕСТИТЬ ВтСотрудники
	               |ИЗ
	               |	Справочник.ЭП_Сотрудники КАК ЭП_Сотрудники
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭП_Сотрудники.РасчетныеПрофили КАК ЭП_СотрудникиРасчетныеПрофили
	               |		ПО (ЭП_СотрудникиРасчетныеПрофили.Ссылка = ЭП_Сотрудники.Ссылка)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
	               |			ПО (ПользователиКонтактнаяИнформация.Ссылка = Пользователи.Ссылка)
	               |		ПО (Пользователи.ЭП_Сотрудник = ЭП_Сотрудники.Ссылка)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВтДепартаментыРуководители КАК ВтДепартаментыРуководители
	               |		ПО ЭП_Сотрудники.Департамент = ВтДепартаментыРуководители.Департамент
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭП_Организации КАК ЭП_Организации
	               |		ПО ЭП_Сотрудники.Организация = ЭП_Организации.Ссылка
	               |ГДЕ
	               |	НЕ ЭП_Сотрудники.ПометкаУдаления
	               |	И НЕ ЭП_Сотрудники.ДоговорГПХ
	               |	И ЭП_Сотрудники.СтатусСотрудника <> ЗНАЧЕНИЕ(Перечисление.ЭП_СтатусыСотрудников.Уволен)
	               |	И ПользователиКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЭП_Сотрудники.Ссылка,
	               |	ЭП_Сотрудники.Фамилия,
	               |	ЭП_Сотрудники.Имя,
	               |	ЭП_Сотрудники.Отчество,
	               |	Пользователи.Ссылка,
	               |	ВтДепартаментыРуководители.ФИОРуководителя,
	               |	ВтДепартаментыРуководители.ДолжностьРуководителя,
	               |	ЭП_Организации.ИНН,
	               |	ЭП_Сотрудники.НеУчаствуетВПЗК,
	               |	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЭП_Сотрудники.Должность)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Сотрудник
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВтСотрудники.Сотрудник КАК Сотрудник,
	               |	ВтСотрудники.Фамилия КАК Фамилия,
	               |	ВтСотрудники.Имя КАК Имя,
	               |	ВтСотрудники.Отчество КАК Отчество,
	               |	ВтСотрудники.Должность КАК Должность,
	               |	ЭП_СотрудникиРасчетныеПрофили.Профиль КАК Профиль,
	               |	ВтСотрудники.Пользователь КАК Пользователь,
	               |	ВтСотрудники.ФИОРуководителя КАК ФИОРуководителя,
	               |	ВтСотрудники.ДолжностьРуководителя КАК ДолжностьРуководителя,
	               |	ВтСотрудники.АдресЭП КАК АдресЭП,
	               |	ВтСотрудники.ИННОрганизации КАК ИННОрганизации
	               |ПОМЕСТИТЬ ВтСотрудникиСПрофилем
	               |ИЗ
	               |	ВтСотрудники КАК ВтСотрудники
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭП_Сотрудники.РасчетныеПрофили КАК ЭП_СотрудникиРасчетныеПрофили
	               |		ПО ВтСотрудники.Сотрудник = ЭП_СотрудникиРасчетныеПрофили.Ссылка
	               |ГДЕ
	               |	НЕ ВтСотрудники.НеУчаствуетВПЗК
	               |	И ВтСотрудники.КоличествоПрофилей = 1
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВтСотрудникиСПрофилем.Сотрудник КАК Сотрудник,
	               |	ВтСотрудникиСПрофилем.Фамилия КАК Фамилия,
	               |	ВтСотрудникиСПрофилем.Имя КАК Имя,
	               |	ВтСотрудникиСПрофилем.Отчество КАК Отчество,
	               |	ВтСотрудникиСПрофилем.Должность КАК Должность,
	               |	ВтСотрудникиСПрофилем.Пользователь КАК Пользователь,
	               |	ВЫБОР
	               |		КОГДА ДЛИНАСТРОКИ(ЭП_ПрофилиРасчетаРезультативностиПоказатели.Показатель.НаименованиеПолное) = 0
	               |			ТОГДА ЭП_ПрофилиРасчетаРезультативностиПоказатели.Показатель.Наименование
	               |		ИНАЧЕ ЭП_ПрофилиРасчетаРезультативностиПоказатели.Показатель.НаименованиеПолное
	               |	КОНЕЦ КАК Показатели,
	               |	ВтСотрудникиСПрофилем.ФИОРуководителя КАК ФИОРуководителя,
	               |	ВтСотрудникиСПрофилем.ДолжностьРуководителя КАК ДолжностьРуководителя,
	               |	ВтСотрудникиСПрофилем.АдресЭП КАК АдресЭП,
	               |	ВтСотрудникиСПрофилем.ИННОрганизации КАК ИННОрганизации,
	               |	NULL КАК Задание
	               |ИЗ
	               |	ВтСотрудникиСПрофилем КАК ВтСотрудникиСПрофилем
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭП_ПрофилиРасчетаРезультативности.Показатели КАК ЭП_ПрофилиРасчетаРезультативностиПоказатели
	               |		ПО ВтСотрудникиСПрофилем.Профиль = ЭП_ПрофилиРасчетаРезультативностиПоказатели.Ссылка
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВтСотрудники.Сотрудник,
	               |	ВтСотрудники.Фамилия,
	               |	ВтСотрудники.Имя,
	               |	ВтСотрудники.Отчество,
	               |	ВтСотрудники.Должность,
	               |	ВтСотрудники.Пользователь,
	               |	NULL,
	               |	ВтСотрудники.ФИОРуководителя,
	               |	ВтСотрудники.ДолжностьРуководителя,
	               |	ВтСотрудники.АдресЭП,
	               |	ВтСотрудники.ИННОрганизации,
	               |	ВтСлужебныеЗадания.Задание
	               |ИЗ
	               |	ВтСлужебныеЗадания КАК ВтСлужебныеЗадания
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСотрудники КАК ВтСотрудники
	               |		ПО ВтСлужебныеЗадания.Сотрудник = ВтСотрудники.Сотрудник
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Задание УБЫВ
	               |ИТОГИ ПО
	               |	Сотрудник";	 
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Возврат Запрос.Выполнить();  

КонецФункции

Функция ПолучитьПараметрыПисьма(АдресЭП, Фамилия, Имя, Отчество, Период, АдресХранилищаПечатнойФормы)
	
	ПараметрыПисьма = Новый Структура;
	ПараметрыПисьма.Вставить("Кому", АдресЭП);  
	ПараметрыПисьма.Вставить("Тема", "Служебное задание");
	ПараметрыПисьма.Вставить("Тело", "К ознакомлению прилагаются следующие задачи (цели).");
	МассивВложений = Новый Массив; 
	ИмяФайла = СтрШаблон("Служебное_задание_%1_%2_%3_на_%4.pdf", Фамилия, Имя, Отчество, Период);
	МассивВложений.Добавить(Новый Структура("АдресВоВременномХранилище, Представление", АдресХранилищаПечатнойФормы, ИмяФайла));
	ПараметрыПисьма.Вставить("Вложения", МассивВложений);
	
	Возврат ПараметрыПисьма;  
	
КонецФункции

#КонецОбласти