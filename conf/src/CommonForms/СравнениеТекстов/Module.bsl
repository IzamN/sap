#Область Переменные

&НаКлиенте
Перем ЗакладкиРедактора;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВременныйФайл = ПолучитьИмяВременногоФайла();
	СоздатьКаталог(ВременныйФайл);

	Попытка
		ДвоичныеДанные = ПолучитьОбщийМакет("РедакторТекстов");
		ЧтениеZipФайла = Новый ЧтениеZipФайла(ДвоичныеДанные.ОткрытьПотокДляЧтения());
		Для Каждого ЭлементZip Из ЧтениеZipФайла.Items Цикл
			ЧтениеZipФайла.Извлечь(ЭлементZip, ВременныйФайл, РежимВосстановленияПутейФайловZIP.Восстанавливать);
			ДвоичныеДанные = Новый ДвоичныеДанные(
				ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ВременныйФайл)+ ЭлементZip.ПолноеИмя);
			СсылкаРедактора = ПолучитьНавигационнуюСсылкуИнформационнойБазы()
				+ "/" + ПоместитьВоВременноеХранилище(ДвоичныеДанные, UUID) + "&localeCode=" + Лев(ТекущийЯзыкСистемы(), 2);
		КонецЦикла;	 
	Исключение
		СсылкаРедактора = "";
	КонецПопытки;
		
	УдалитьФайлы(ВременныйФайл);
	
	Если Параметры.Свойство("ТекстДляСравнения1") Тогда
		ТекстДляСравнения1 = Параметры.ТекстДляСравнения1;
	КонецЕсли;	
	Если Параметры.Свойство("ТекстДляСравнения2") Тогда
		ТекстДляСравнения2 = Параметры.ТекстДляСравнения2;
	КонецЕсли;	
	Если Параметры.Свойство("ОписаниеТекста1") Тогда
		ОписаниеТекста1 = Параметры.ОписаниеТекста1;
	КонецЕсли;	
	Если Параметры.Свойство("ОписаниеТекста2") Тогда
		ОписаниеТекста2 = Параметры.ОписаниеТекста2;
	КонецЕсли;	
	Если Параметры.Свойство("ВыполнитьСравнениеДвухТекстов") Тогда
		ВыполнитьСравнениеДвухТекстов = Параметры.ВыполнитьСравнениеДвухТекстов;
	КонецЕсли;	
	Если Параметры.Свойство("ИмяФайлаРепозитория") Тогда
		ИмяФайлаРепозитория = Параметры.ИмяФайлаРепозитория;
		Заголовок = ИмяФайлаРепозитория;
	КонецЕсли;	
	
	ПоказыватьРазличияДвумяСтолбцами = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"Тестирование", 
		"ПоказыватьРазличияДвумяСтолбцами");
	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СсылкаРедактораДокументСформирован(Элемент)
	ИнициализироватьРедактор();
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьРазличияДвумяСтолбцамиПриИзменении(Элемент)
	
	ЗакладкиРедактора.current.editor.setSideBySide(ПоказыватьРазличияДвумяСтолбцами);
	СохранитьНастройкиСервер(ПоказыватьРазличияДвумяСтолбцами);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиКПредыдущемуИзменению(Команда)
	ЗакладкиРедактора.previousDiff();
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКСледующемуИзменению(Команда)
	ЗакладкиРедактора.nextDiff();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИнициализироватьРедактор()
	Представление = Элементы.Редактор.Document.defaultView;
	ЗакладкиРедактора = Представление.createVanessaTabs();
	
	ЗакладкиРедактора.diff(
		ТекстДляСравнения1,
		ОписаниеТекста1,
		"Файл1",
		ТекстДляСравнения2,
		ОписаниеТекста2,
		"Файл2",
		СтрШаблон(НСтр("ru = 'Сравнивается <%1> с <%2>'"), ОписаниеТекста1, ОписаниеТекста2),
		0,
		Истина, Истина); 
		
	ЗакладкиРедактора.current.editor.setSideBySide(ПоказыватьРазличияДвумяСтолбцами);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиСервер(ПоказыватьРазличияДвумяСтолбцами)
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("Тестирование",
		                                                 "ПоказыватьРазличияДвумяСтолбцами",
														 ПоказыватьРазличияДвумяСтолбцами
														 ,
														 ,
														 ,
														 Истина);
КонецПроцедуры

#КонецОбласти