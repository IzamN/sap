#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьИнформацию();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьРеквизиты();
	
КонецПроцедуры 

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьИнформацию();
	ОбновитьРеквизиты();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьИнформацию()
	
	ОценкиОтсутствуют = Истина;
	НадписьОтсутствияОценок = "Нет оценок по профилю";
	НадписьПрисутствияОценок = "Оценка за текущий период выставлена";
	
	Сотрудник = ПолучитьСотрудникаПоТекущемуПользователю();
	
	РезультатЗапросаПрофилиПользователя = ПолучитьПрофилиТекущегоПользователя(Сотрудник);
	
	Если РезультатЗапросаПрофилиПользователя.Пустой() Тогда
		НадписьОтсутствияОценок = "Не назначен профиль, обратитесь к Германову В.С.";
		Профили = "";
		ОценкиОтсутствуют = Истина;
		Возврат;
	КонецЕсли;
	
	РезультатЗапроса = ПолучитьДанныеОбОценках(Сотрудник); 
	
	Если РезультатЗапроса.Пустой() Тогда
		ОценкиОтсутствуют = Ложь;
	Иначе
		ОценкиОтсутствуют = Истина;
		ПрофилиСтрока = "Профили: ";
		
		УникальныеПрофили = РезультатЗапроса.Выгрузить();
		УникальныеПрофили.Свернуть("ПрофильПредставление");
		УникальныеПрофили = УникальныеПрофили.ВыгрузитьКолонку("ПрофильПредставление");
		
		Профили = ПрофилиСтрока + СтрСоединить(УникальныеПрофили, ", ");
	КонецЕсли; 
	
КонецПроцедуры 

&НаСервере
Функция ПолучитьДанныеОбОценках(Сотрудник)
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос; 
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭП_СотрудникиРасчетныеПрофили.Профиль КАК Профиль
	|ПОМЕСТИТЬ втПрофилиСотрудника
	|ИЗ
	|	Справочник.ЭП_Сотрудники.РасчетныеПрофили КАК ЭП_СотрудникиРасчетныеПрофили
	|ГДЕ
	|	ЭП_СотрудникиРасчетныеПрофили.Ссылка = &Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭП_ПрофилиРасчетаРезультативностиПоказатели.Показатель КАК Показатель,
	|	втПрофилиСотрудника.Профиль КАК Профиль
	|ПОМЕСТИТЬ втПоказатели
	|ИЗ
	|	втПрофилиСотрудника КАК втПрофилиСотрудника
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭП_ПрофилиРасчетаРезультативности.Показатели КАК ЭП_ПрофилиРасчетаРезультативностиПоказатели
	|		ПО втПрофилиСотрудника.Профиль = ЭП_ПрофилиРасчетаРезультативностиПоказатели.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭП_ЭффективностьСотрудников.Сотрудник КАК Сотрудник,
	|	ЭП_ЭффективностьСотрудников.ПрофильРасчета.Представление КАК ПрофильПредставление,
	|	ЭП_ЭффективностьСотрудников.ПрофильРасчета КАК ПрофильРасчета,
	|	ЭП_ЭффективностьСотрудников.Показатель КАК Показатель
	|ПОМЕСТИТЬ втРегистрЗаписиПоСотруднику
	|ИЗ
	|	РегистрСведений.ЭП_ЭффективностьСотрудников КАК ЭП_ЭффективностьСотрудников
	|ГДЕ
	|	ЭП_ЭффективностьСотрудников.ПериодОценивания МЕЖДУ &Начало И &Конец
	|	И ЭП_ЭффективностьСотрудников.Сотрудник = &Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПоказатели.Профиль КАК Профиль,
	|	втПоказатели.Показатель КАК Показатель,
	|	втПоказатели.Профиль.Представление КАК ПрофильПредставление
	|ИЗ
	|	втПоказатели КАК втПоказатели
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРегистрЗаписиПоСотруднику КАК втРегистрЗаписиПоСотруднику
	|		ПО втПоказатели.Профиль = втРегистрЗаписиПоСотруднику.ПрофильРасчета
	|			И втПоказатели.Показатель = втРегистрЗаписиПоСотруднику.Показатель
	|ГДЕ
	|	втРегистрЗаписиПоСотруднику.ПрофильРасчета ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Начало", НачалоМесяца(ДобавитьМесяц(ТекущаяДатаСеанса(), -1)));
	Запрос.УстановитьПараметр("Конец", КонецМесяца(ДобавитьМесяц(ТекущаяДатаСеанса(), -1)));
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	РезультатЗапроса = Запрос.Выполнить(); 
	УстановитьПривилегированныйРежим(Ложь);
	Возврат РезультатЗапроса;
	
КонецФункции

&НаСервере
Функция ПолучитьПрофилиТекущегоПользователя(Сотрудник)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭП_СотрудникиРасчетныеПрофили.Профиль.Представление КАК ПрофильПредставление
	|ИЗ
	|	Справочник.ЭП_Сотрудники.РасчетныеПрофили КАК ЭП_СотрудникиРасчетныеПрофили
	|ГДЕ
	|	ЭП_СотрудникиРасчетныеПрофили.Ссылка = &ТекущийСотрудник";
	
	Запрос.УстановитьПараметр("ТекущийСотрудник", Сотрудник);
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса;
	
КонецФункции

&НаСервере 
Функция ПолучитьСотрудникаПоТекущемуПользователю()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Пользователи.ЭП_Сотрудник КАК Сотрудник
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Пользователи.ТекущийПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Сотрудник;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьРеквизиты()
	
	Если ОценкиОтсутствуют Тогда 
		Элементы.НадписьОтсутствияОценок.Видимость = Истина;
		Элементы.НадписьПрисутствияОценок.Видимость = Ложь;
		Элементы.Профили.Видимость = Истина;
	Иначе
		Элементы.НадписьОтсутствияОценок.Видимость = Ложь; 
		Элементы.НадписьПрисутствияОценок.Видимость = Истина;
		Элементы.Профили.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
