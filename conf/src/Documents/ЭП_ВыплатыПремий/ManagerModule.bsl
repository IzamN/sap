#Область ПрограммныйИнтерфейс
// Заполняет список команд печати.
//
// Параметры:
// КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
    
    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.Идентификатор = "ЭП_ВыплатыПремий";
    КомандаПечати.Представление = НСтр("ru = 'Выплаты премий'");
   
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	// Печать направления
    ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ЭП_ВыплатыПремий");
    Если ПечатнаяФорма <> Неопределено Тогда
           
        // описание печатной формы
        ПечатнаяФорма.ТабличныйДокумент = ПечатьРасчетаПоказателей(МассивОбъектов, ОбъектыПечати);
        ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Выплаты премий'");
        ПечатнаяФорма.ПолныйПутьКМакету = "Документ.ЭП_ВыплатыПремий.ПФ_MXL_ВыплатыПремий";
		//ПечатнаяФорма.ИмяФайлаПечатнойФормы = ИменаФайлов;
    КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьРасчетаПоказателей(МассивОбъектов, ОбъектыПечати)
	
	ТабДок = Новый ТабличныйДокумент;
    ТабДок.КлючПараметровПечати = "ПараметрыПечати_ЭП_ВыплатыПремий";
	
	
	Макет = Документы.ЭП_ВыплатыПремий.ПолучитьМакет("ПФ_MXL_ВыплатыПремий");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЭП_ВыплатыПремий.Дата КАК Дата,
	|	ЭП_ВыплатыПремий.Комментарий КАК Комментарий,
	|	ЭП_ВыплатыПремий.Номер КАК Номер,
	|	ЭП_ВыплатыПремий.Ответственный КАК Ответственный,
	|	ЭП_ВыплатыПремий.Проект КАК Проект,
	|	ЭП_ВыплатыПремий.Сотрудники.(
	|		НомерСтроки КАК НомерСтроки,
	|		Пользователь КАК Пользователь,
	|		ПроектныйКоэффициент КАК ПроектныйКоэффициент,
	|		СреднийКоэффициентПЗК КАК СреднийКоэффициентПЗК,
	|		ПремияПоЭтапу КАК ПремияПоЭтапу,
	|		ПроектнаяПремия КАК ПроектнаяПремия,
	|		ИтогБезУчетаПроектногоКоэффициента КАК ИтогБезУчетаПроектногоКоэффициента,
	|		ИтогСУчетомПроектногоКоэффициента КАК ИтогСУчетомПроектногоКоэффициента,
	|		Этап КАК Этап
	|	) КАК Сотрудники,
	|	ЭП_ВыплатыПремий.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЭП_ВыплатыПремий КАК ЭП_ВыплатыПремий
	|ГДЕ
	|	ЭП_ВыплатыПремий.Ссылка В(&Ссылка)";
	Запрос.Параметры.Вставить("Ссылка", МассивОбъектов);
	Результат = Запрос.Выполнить();
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСотрудникиШапка = Макет.ПолучитьОбласть("СотрудникиШапка");
	ОбластьСотрудники = Макет.ПолучитьОбласть("Сотрудники");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьИтог = Макет.ПолучитьОбласть("Всего");

	ТабДок.Очистить();

	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		ТабДок.Вывести(ОбластьЗаголовок);

		Шапка.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(Шапка, Выборка.Уровень());
        
		ТабДок.Вывести(ОбластьСотрудникиШапка);
		ВыборкаСотрудники = Выборка.Сотрудники.Выбрать();
		ОстатокПЗКИтог = 0;
		ПроектнаяПремияИтог = 0;
		ИПФИтог = 0;
		СуммаИтог = 0;		
		Пока ВыборкаСотрудники.Следующий() Цикл
			
			ОбластьСотрудники.Параметры.Заполнить(ВыборкаСотрудники);
			ТабДок.Вывести(ОбластьСотрудники, ВыборкаСотрудники.Уровень());
			ОстатокПЗКИтог = ОстатокПЗКИтог + ВыборкаСотрудники.ПремияПоЭтапу;
			ПроектнаяПремияИтог = ПроектнаяПремияИтог + ВыборкаСотрудники.ПроектнаяПремия;
			ИПФИтог = ИПФИтог + ВыборкаСотрудники.ИтогБезУчетаПроектногоКоэффициента;
			СуммаИтог = СуммаИтог + ВыборкаСотрудники.ИтогСУчетомПроектногоКоэффициента;
			
		КонецЦикла;

		ОбластьИтог.Параметры.ОстатокПЗКВсего = ОстатокПЗКИтог;
		ОбластьИтог.Параметры.ПроектнаяПремияВсего = ПроектнаяПремияИтог;
		ОбластьИтог.Параметры.ИПФВсего = ИПФИтог;
		ОбластьИтог.Параметры.СуммаВсего = СуммаИтог;
		ТабДок.Вывести(ОбластьИтог);
		Подвал.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(Подвал);

		ВставлятьРазделительСтраниц = Истина;	
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;

КонецФункции

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Проект)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецЕсли