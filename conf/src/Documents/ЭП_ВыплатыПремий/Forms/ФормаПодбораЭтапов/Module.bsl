
#Область ОбработчикиСобытийФормы 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Проект") Тогда
		Проект = Параметры.Проект; 
	КонецЕсли;
	ВидДокумента = Параметры.ВидДокумента;
	Если ВидДокумента = Перечисления.ЭП_ВидыДокументаВыплатаПремий.ВыплатаПоСопровждению Тогда
		Элементы.СписокЭтап.Заголовок = "ТехническийПроект";
		Элементы.ВыбранныеЭтапыЭтап.Заголовок = "ТехническийПроект";
		Элементы.ВыбранныеЭтапы.Заголовок = "Выбранные технические проекты";
	КонецЕсли;
	Если Параметры.Свойство("ВидВыплаты") Тогда
	
		ВидВыплаты = Параметры.ВидВыплаты;		
	
	КонецЕсли;
	Период = Параметры.Период;
	ОбновитьСписокВыбораЭтапов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы 

&НаКлиенте
Процедура ВыполненныеЭтапыПриИзменении(Элемент)
	
	ВыполненныеЭтапыПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыделенныеСтроки = ВыбранныеЭтапы;
	РезультатВыбора = Новый Структура; 
    АдресВоВременномХранилище = ПоместитьВыбранныеЭтапыВоВременноеХранилище();
	РезультатВыбора.Вставить("АдресТаблицыЭтаповИАктов", АдресВоВременномХранилище);
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервере
Функция ПоместитьВыбранныеЭтапыВоВременноеХранилище()

	ТаблицаЭтаповИАктов = ВыбранныеЭтапы.Выгрузить();   
	Возврат ПоместитьВоВременноеХранилище(ТаблицаЭтаповИАктов);	

КонецФункции

&НаСервере
Процедура ВыполненныеЭтапыПриИзмененииНаСервере()
	ОбновитьСписокВыбораЭтапов();
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокВыбораЭтапов()
	 
	Если ВидДокумента = Перечисления.ЭП_ВидыДокументаВыплатаПремий.ОбычнаяВыплата Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭП_РеализацияОбороты.Этап КАК Этап,
		|	ЭП_РеализацияОбороты.Регистратор КАК Акт
		|ПОМЕСТИТЬ ВТ_ЭтапыСАктами
		|ИЗ
		|	РегистрНакопления.ЭП_Реализация.Обороты(, , Регистратор, НЕ Этап В (&МассивЭтапов)) КАК ЭП_РеализацияОбороты
		|ГДЕ
		|	ЭП_РеализацияОбороты.СуммаОборот > 0
		|	И НЕ ЭП_РеализацияОбороты.Регистратор В (&МассивАктов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЭП_УчетПремийОстатки.Этап КАК Этап,
		|	ВТ_ЭтапыСАктами.Акт КАК Акт
		|ПОМЕСТИТЬ ВТ_Этапы
		|ИЗ
		|	ВТ_ЭтапыСАктами КАК ВТ_ЭтапыСАктами
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЭП_УчетПремий.Остатки(
		|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
		|				Проект = &Проект
		|					И Этап В
		|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|							ВТ_ЭтапыСАктами.Этап КАК Этап
		|						ИЗ
		|							ВТ_ЭтапыСАктами КАК ВТ_ЭтапыСАктами)) КАК ЭП_УчетПремийОстатки
		|		ПО ВТ_ЭтапыСАктами.Этап = ЭП_УчетПремийОстатки.Этап
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_Этапы.Этап КАК Этап,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТехническиеПроекты.Ссылка) КАК КоличествоВсего,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТехническиеПроектыЗакрытые.Ссылка) КАК КоличествоЗакрытых,
		|	ВТ_Этапы.Акт КАК Акт
		|ПОМЕСТИТЬ ВТ_КоличествоТехПроектов
		|ИЗ
		|	ВТ_Этапы КАК ВТ_Этапы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТехническиеПроекты КАК ТехническиеПроекты
		|		ПО (ВТ_Этапы.Этап = ТехническиеПроекты.РазделПроекта
		|				ИЛИ ВТ_Этапы.Этап = ТехническиеПроекты.РазделыПроекта.Раздел)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТехническиеПроекты КАК ТехническиеПроектыЗакрытые
		|		ПО (ВТ_Этапы.Этап = ТехническиеПроектыЗакрытые.РазделПроекта
		|				ИЛИ ВТ_Этапы.Этап = ТехническиеПроектыЗакрытые.РазделыПроекта.Раздел)
		|			И (ТехническиеПроектыЗакрытые.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыТехническихПроектов.Выполнен), ЗНАЧЕНИЕ(Перечисление.СтатусыТехническихПроектов.Отменен)))
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Этапы.Этап,
		|	ВТ_Этапы.Акт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_КоличествоТехПроектов.Этап КАК Этап,
		|	ВЫБОР
		|		КОГДА ВТ_КоличествоТехПроектов.КоличествоВсего = ВТ_КоличествоТехПроектов.КоличествоЗакрытых
		|			ТОГДА ""Закрыт""
		|		ИНАЧЕ ""Не закрыт""
		|	КОНЕЦ КАК Статус,
		|	ВТ_КоличествоТехПроектов.Акт КАК Акт
		|ИЗ
		|	ВТ_КоличествоТехПроектов КАК ВТ_КоличествоТехПроектов";
		
		
	Иначе
	    Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЭП_РеализацияОбороты.Этап КАК Этап,
		|	ЭП_РеализацияОбороты.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТ_ЭтапыСАктами
		|ИЗ
		|	РегистрНакопления.ЭП_Реализация.Обороты(, , Регистратор, Проект = &Проект) КАК ЭП_РеализацияОбороты
		|ГДЕ
		|	НЕ ЭП_РеализацияОбороты.Регистратор В (&МассивАктов)
		|	И ЭП_РеализацияОбороты.СуммаОборот > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЭП_УчетПремийПоДокументамОстатки.ТехническийПроект КАК Этап,
		|	ЭП_УчетПремийПоДокументамОстатки.ТехническийПроект.Статус КАК Статус,
		|	ВТ_ЭтапыСАктами.Регистратор КАК Акт
		|ИЗ
		|	ВТ_ЭтапыСАктами КАК ВТ_ЭтапыСАктами
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЭП_УчетПремийПоДокументам.Остатки(
		|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
		|				Проект = &Проект
		|					И НЕ ТехническийПроект В (&МассивЭтапов)) КАК ЭП_УчетПремийПоДокументамОстатки
		|		ПО ВТ_ЭтапыСАктами.Регистратор = ЭП_УчетПремийПоДокументамОстатки.ТехническийПроект.ЭП_РеализацияУслуг
		|ГДЕ
		|	ЭП_УчетПремийПоДокументамОстатки.ТехническийПроект.ЭП_РеализацияУслуг.Этап В
		|			(ВЫБРАТЬ
		|				ВТ_ЭтапыСАктами.Этап КАК Этап
		|			ИЗ
		|				ВТ_ЭтапыСАктами КАК ВТ_ЭтапыСАктами)";
		
	КонецЕсли;
	Запрос.УстановитьПараметр("Проект", Проект);
	Запрос.УстановитьПараметр("Статус", Перечисления.СтатусыТехническихПроектов.Выполнен);
	Запрос.УстановитьПараметр("Дата", Период);
	Запрос.УстановитьПараметр("МассивЭтапов", ВыбранныеЭтапы.Выгрузить(,"Этап"));  
	Запрос.УстановитьПараметр("МассивАктов", ВыбранныеЭтапы.Выгрузить(,"Акт"));
    Список.Загрузить(Запрос.Выполнить().Выгрузить());
	

КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Для Каждого Строка Из ВыбраннаяСтрока Цикл
		НоваяСтрока = ВыбранныеЭтапы.Добавить();
		НоваяСтрока.Этап = Элементы.Список.ДанныеСтроки(Строка).Этап; 
		НоваяСтрока.Акт = Элементы.Список.ДанныеСтроки(Строка).Акт; 
	КонецЦикла;
	
	ОбновитьСписокВыбораЭтапов();
КонецПроцедуры  

&НаКлиенте
Процедура ВыбранныеЭтапыПриИзменении(Элемент)
	ОбновитьСписокВыбораЭтапов();
КонецПроцедуры

#КонецОбласти

