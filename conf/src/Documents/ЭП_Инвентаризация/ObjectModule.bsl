#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	РежимРаботы = Перечисления.ЭП_ВидыПоступлений.ПоСкладу;
		
	Если Ответственный.Ссылка.Пустая() ИЛИ НЕ ЗначениеЗаполнено(Ответственный) Тогда
        Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
    КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты) 
	
	Если РежимРаботы = Перечисления.ЭП_ВидыПоступлений.ПоСкладу Тогда
		ПроверяемыеРеквизиты.Добавить("Склад");
	Иначе
		ПроверяемыеРеквизиты.Добавить("Сотрудник");
	КонецЕсли;    
	
КонецПроцедуры  

Процедура ПриЗаписи(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", СписокНоменклатуры.Выгрузить());
	Запрос.Текст = "ВЫБРАТЬ
	|	ТабЧасть.Номенклатура КАК Номенклатура,
	|	ТабЧасть.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ТабЧасть.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ТабЧасть.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТ_ТабЧасть
	|ИЗ
	|	&Таблица КАК ТабЧасть
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТабЧасть.Номенклатура КАК Номенклатура,
	|	ВТ_ТабЧасть.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ВТ_ТабЧасть.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ВТ_ТабЧасть.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВТ_ТабЧасть КАК ВТ_ТабЧасть
	|ГДЕ
	|	(ВТ_ТабЧасть.Номенклатура, ВТ_ТабЧасть.ИнвентарныйНомер, ВТ_ТабЧасть.ВидНоменклатуры) В
	|			(ВЫБРАТЬ
	|				ВТ.Номенклатура,
	|				ВТ.ИнвентарныйНомер,
	|				ВТ.ВидНоменклатуры
	|			ИЗ
	|				ВТ_ТабЧасть КАК ВТ
	|			СГРУППИРОВАТЬ ПО
	|				ВТ.Номенклатура,
	|				ВТ.ИнвентарныйНомер,
	|				ВТ.ВидНоменклатуры
	|			ИМЕЮЩИЕ
	|				КОЛИЧЕСТВО(ВТ.НомерСтроки) > 1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|ИТОГИ ПО
	|	Номенклатура";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выб_Номенклатура = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Выб_Номенклатура.Следующий() Цикл
			Выборка = Выб_Номенклатура.Выбрать();
			Сообщение = "";
			Пока Выборка.Следующий() Цикл
				Сообщение = Сообщение + ?(ПустаяСтрока(Сообщение), "", ", ") + Выборка.НомерСтроки;
			КонецЦикла;
			Сообщение = "Обнаружено дублирование строк: " + Сообщение;
			Сообщить(Сообщение);
		КонецЦикла;
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Движения.ЭП_КачествоНоменклатуры.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭП_ИнвентаризацияСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЭП_ИнвентаризацияСписокНоменклатуры.КачествоНоменклатуры КАК КачествоНоменклатуры,
	|	ЭП_ИнвентаризацияСписокНоменклатуры.ИнвентарныйНомер КАК ИнвентарныйНомер
	|ИЗ
	|	Документ.ЭП_Инвентаризация.СписокНоменклатуры КАК ЭП_ИнвентаризацияСписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЭП_КачествоНоменклатуры.СрезПоследних(&Дата, ) КАК ЭП_КачествоНоменклатурыСрезПоследних
	|		ПО ЭП_ИнвентаризацияСписокНоменклатуры.Номенклатура = ЭП_КачествоНоменклатурыСрезПоследних.Номенклатура
	|			И ЭП_ИнвентаризацияСписокНоменклатуры.ИнвентарныйНомер = ЭП_КачествоНоменклатурыСрезПоследних.ИнвентарныйНомер
	|ГДЕ
	|	ЭП_ИнвентаризацияСписокНоменклатуры.Ссылка = &Ссылка
	|	И (ЭП_КачествоНоменклатурыСрезПоследних.КачествоНоменклатуры ЕСТЬ NULL
	|			ИЛИ ЭП_ИнвентаризацияСписокНоменклатуры.КачествоНоменклатуры <> ЭП_КачествоНоменклатурыСрезПоследних.КачествоНоменклатуры)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл  
		Движение = Движения.ЭП_КачествоНоменклатуры.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура; 
		Движение.ИнвентарныйНомер = Выборка.ИнвентарныйНомер;
		Движение.КачествоНоменклатуры = Выборка.КачествоНоменклатуры;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти