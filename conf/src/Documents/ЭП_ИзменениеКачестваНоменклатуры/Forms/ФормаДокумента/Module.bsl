#Область  АвтоматическоеЗаполнениеПоляКачествоНоменклатуры 

&НаСервере
Функция ПолучитьКачествоНоменклатуры(Номенклатура, ИнвентарныйНомер)  
	
	Отбор = Новый Структура("Номенклатура, ИнвентарныйНомер", Номенклатура, ИнвентарныйНомер);
	Возврат РегистрыСведений.ЭП_КачествоНоменклатуры.ПолучитьПоследнее(ТекущаяДатаСеанса(), Отбор).КачествоНоменклатуры;
	
КонецФункции

&НаКлиенте
Процедура УстановитьКачествоНоменклатуры()
	СтрокаСписокНоменклатуры = Элементы.СписокНоменклатуры.ТекущиеДанные;  
	
	ВозможныеИнвентарныеНомера = ПолучитьДанныеВыбораПоИнвентарномуНомеру(СтрокаСписокНоменклатуры.ИнвентарныйНомер,
	СтрокаСписокНоменклатуры.Номенклатура, СтрокаСписокНоменклатуры.ВидНоменклатуры, Ложь); 
	
	Если ВозможныеИнвентарныеНомера.НайтиПоЗначению(СтрокаСписокНоменклатуры.ИнвентарныйНомер) <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(СтрокаСписокНоменклатуры.Номенклатура) И  ЗначениеЗаполнено(СтрокаСписокНоменклатуры.ИнвентарныйНомер) Тогда
			КачествоНоменклатуры = ПолучитьКачествоНоменклатуры(СтрокаСписокНоменклатуры.Номенклатура, СтрокаСписокНоменклатуры.ИнвентарныйНомер);
			Если КачествоНоменклатуры.Пустая() Тогда
				СтрокаСписокНоменклатуры.КачествоНоменклатуры = ПредопределенноеЗначение("Справочник.ЭП_КачествоНоменклатуры.Новый");
			Иначе
				СтрокаСписокНоменклатуры.КачествоНоменклатуры = КачествоНоменклатуры; 
			КонецЕсли; 
		КонецЕсли;
	Иначе
		
		СтрокаСписокНоменклатуры.ИнвентарныйНомер = Неопределено;
		СтрокаСписокНоменклатуры.КачествоНоменклатуры = Неопределено; 
		
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыНоменклатураПриИзменении(Элемент)
	УстановитьКачествоНоменклатуры();	
КонецПроцедуры 

&НаКлиенте
Процедура СписокНоменклатурыИнвентарныйНомерПриИзменении(Элемент)
	УстановитьКачествоНоменклатуры();
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура СписокНоменклатурыВидНоменклатурыНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)	
	СтандартнаяОбработка = Ложь;
	ПараметрыПередачи = Элементы.СписокНоменклатуры.ТекущиеДанные.Номенклатура;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОтборПоВиду", ПараметрыПередачи);
	ОткрытьФорму("Справочник.ЭП_ВидНоменлкатуры.ФормаВыбора",ПараметрыФормы, Элемент);	
КонецПроцедуры   

&НаСервереБезКонтекста
Функция СписокНоменклатурыВидНоменклатурыАвтоПодборНаСервере(СтрокаПоиска)
	Возврат Справочники.ЭП_ВидНоменлкатуры.ПолучитьДанныеВыбора(Новый Структура("СтрокаПоиска, ВводПоСтрокеРасширенный", СтрокаПоиска, Истина));
КонецФункции

&НаКлиенте
Процедура СписокНоменклатурыВидНоменклатурыАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = СписокНоменклатурыВидНоменклатурыАвтоПодборНаСервере(Текст);	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыПередачи = Элементы.СписокНоменклатуры.ТекущиеДанные.ВидНоменклатуры;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОтборПоВиду", ПараметрыПередачи);
	ОткрытьФорму("Справочник.ЭП_Номенклатура.ФормаВыбора",ПараметрыФормы, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыИнвентарныйНомерАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	Если ПустаяСтрока(Текст) Тогда 
		ДанныеВыбора = ПолучитьДанныеВыбораПоИнвентарномуНомеру(Элементы.СписокНоменклатуры.ТекущиеДанные.ИнвентарныйНомер,
		Элементы.СписокНоменклатуры.ТекущиеДанные.Номенклатура, Элементы.СписокНоменклатуры.ТекущиеДанные.ВидНоменклатуры, 
		СтандартнаяОбработка); 
	Иначе
		ДанныеВыбора = ПолучитьДанныеВыбораПоИнвентарномуНомеру(Текст, Элементы.СписокНоменклатуры.ТекущиеДанные.Номенклатура, 
		Элементы.СписокНоменклатуры.ТекущиеДанные.ВидНоменклатуры, СтандартнаяОбработка);		
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыИнвентарныйНомерОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	СписокНоменклатурыИнвентарныйНомерПолучениеНоменклатуры(ВыбранноеЗначение, Элементы.СписокНоменклатуры.ТекущаяСтрока);
КонецПроцедуры 

&НаСервере
Процедура СписокНоменклатурыИнвентарныйНомерПолучениеНоменклатуры(Текст, ИндексСтроки);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭП_ОстаткиНаСкладеОстатки.Номенклатура КАК Номенклатура,
	|	ЭП_ОстаткиНаСкладеОстатки.Номенклатура.ВидНоменклатуры КАК НоменклатураВидНоменклатуры
	|ИЗ
	|	РегистрНакопления.ЭП_ОстаткиНаСкладе.Остатки КАК ЭП_ОстаткиНаСкладеОстатки
	|ГДЕ
	|	ЭП_ОстаткиНаСкладеОстатки.ИнвентарныйНомер = &ИнвентарныйНомер
	|	И ЭП_ОстаткиНаСкладеОстатки.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЭП_ОстаткиНаСотрудникахОстатки.Номенклатура,
	|	ЭП_ОстаткиНаСотрудникахОстатки.Номенклатура.ВидНоменклатуры
	|ИЗ
	|	РегистрНакопления.ЭП_ОстаткиНаСотрудниках.Остатки КАК ЭП_ОстаткиНаСотрудникахОстатки
	|ГДЕ
	|	ЭП_ОстаткиНаСотрудникахОстатки.ИнвентарныйНомер = &ИнвентарныйНомер
	|	И ЭП_ОстаткиНаСотрудникахОстатки.КоличествоОстаток > 0";
	
	Запрос.УстановитьПараметр("ИнвентарныйНомер", Текст);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		
		СтрокаТЧ = Объект.СписокНоменклатуры.НайтиПоИдентификатору(ИндексСтроки);
		СтрокаТЧ.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
		СтрокаТЧ.ВидНоменклатуры = ВыборкаДетальныеЗаписи.Номенклатура.ВидНоменклатуры; 
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьДанныеВыбораПоИнвентарномуНомеру(ИнвентарныйНомер, Номенклатура, ВидНоменклатуры, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыПодбора = Новый Структура("Отбор,СтрокаПоиска,ВыборГруппИЭлементов", Новый Структура(), "", 
										ПредопределенноеЗначение("ИспользованиеГруппИЭлементов.Элементы"));
	ПараметрыПодбора.Вставить("ДополнительныеПараметры", Новый Структура("Склад, ИнвентарныйНомер, Номенклатура, РежимРаботы, Сотрудник, ВидНоменклатуры", 
										"", ИнвентарныйНомер, Номенклатура, "", "" , ВидНоменклатуры));
	Возврат ПолучитьДанныеВыбора(Тип("СправочникСсылка.ЭП_Номенклатура"), ПараметрыПодбора);  
КонецФункции





