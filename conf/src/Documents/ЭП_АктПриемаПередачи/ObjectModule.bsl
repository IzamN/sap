#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ СтатусДокумента Тогда
	   	Отказ = Истина; 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Документ может быть проведен только после согласования";
		Сообщение.Сообщить();
		Возврат;		
	
	КонецЕсли;
	
	Движения.ЭП_ОстаткиНаСотрудниках.Очистить(); 
	Движения.ЭП_ОстаткиНаСотрудниках.Записывать = Истина; 
	Движения.ЭП_ОстаткиНаСотрудниках.Записать(); 
	
	Движения.ЭП_ОстаткиНаСкладе.Очистить();				
	Движения.ЭП_ОстаткиНаСкладе.Записывать = Истина;
	Движения.ЭП_ОстаткиНаСкладе.Записать(); 

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка); 
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник); 
	Запрос.УстановитьПараметр("Склад", Склад);

	Если ВидОперации = Перечисления.ЭП_ВидыОперций.ПриемНоменклатуры Тогда
		
		Запрос.Текст = ТекстЗапросаПриемНоменклатурыУСотрудника(); 			
		ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ВыборкаДетальныеЗаписи.КоличествоОстатокНаСотруднике = 0 ИЛИ ВыборкаДетальныеЗаписи.КоличествоОстатокНаСкладе > 0 Тогда 
				
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Номенклатура " + ВыборкаДетальныеЗаписи.НоменклатураПредставление + " с инвентарным номером " +
				ВыборкаДетальныеЗаписи.ИнвентарныйНомер + " отсутсвует у сотрудника";
				Сообщение.Сообщить();
				
			ИначеЕсли ВыборкаДетальныеЗаписи.КоличествоОстатокНаСкладе > 0 Тогда
				
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Номенклатура " + ВыборкаДетальныеЗаписи.НоменклатураПредставление + " с инвентарным номером " +
				ВыборкаДетальныеЗаписи.ИнвентарныйНомер + " есть на складе";
				Сообщение.Сообщить();
						
			Иначе

				Движение = Движения.ЭП_ОстаткиНаСотрудниках.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Сотрудник = Сотрудник;
				Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
				Движение.ИнвентарныйНомер = ВыборкаДетальныеЗаписи.ИнвентарныйНомер;
				Движение.Количество = 1; 
				
				Движение = Движения.ЭП_ОстаткиНаСкладе.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Период = Дата;
				Движение.Склад = Склад;
				Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
				Движение.ИнвентарныйНомер = ВыборкаДетальныеЗаписи.ИнвентарныйНомер;
				Движение.Количество = 1;
							
			КонецЕсли;
		КонецЦикла; 
		
	Иначе
		
		Запрос.Текст = ТекстЗапросаПередачаНоменклатурыСотруднику();		
		ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать(); 
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ВыборкаДетальныеЗаписи.КоличествоОстатокНаСотруднике > 0 Тогда
					
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Такая номенклатура "+ ВыборкаДетальныеЗаписи.НоменклатураПредставление +  " с инвентарным номером " +
				ВыборкаДетальныеЗаписи.ИнвентарныйНомер + " есть на сотруднике";
				Сообщение.Сообщить();
				
			ИначеЕсли ВыборкаДетальныеЗаписи.КоличествоОстатокНаСкладе = 0 Тогда
				
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Такая номенклатура "+ ВыборкаДетальныеЗаписи.НоменклатураПредставление + " с инвентарным номером " +
				ВыборкаДетальныеЗаписи.ИнвентарныйНомер + " отсутсвует на складе";
				Сообщение.Сообщить();
		
			Иначе
				
				Движение = Движения.ЭП_ОстаткиНаСотрудниках.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Период = Дата;
				Движение.Сотрудник = Сотрудник;
				Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
				Движение.ИнвентарныйНомер = ВыборкаДетальныеЗаписи.ИнвентарныйНомер;
				Движение.Количество = 1;
				
				Движение = Движения.ЭП_ОстаткиНаСкладе.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Склад = Склад;
				Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
				Движение.ИнвентарныйНомер = ВыборкаДетальныеЗаписи.ИнвентарныйНомер;
				Движение.Количество = 1;
				
			КонецЕсли; 		
		КонецЦикла;
		
	КонецЕсли; 
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Движения.ЭП_КачествоНоменклатуры.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭП_АктПриемаПередачиНоменклатура.Номенклатура КАК Номенклатура,
	|	ЭП_АктПриемаПередачиНоменклатура.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ЭП_АктПриемаПередачиНоменклатура.КачествоНоменклатуры КАК КачествоНоменклатуры
	|ИЗ
	|	Документ.ЭП_АктПриемаПередачи.Номенклатура КАК ЭП_АктПриемаПередачиНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЭП_КачествоНоменклатуры.СрезПоследних(&Дата, ) КАК ЭП_КачествоНоменклатурыСрезПоследних
	|		ПО ЭП_АктПриемаПередачиНоменклатура.Номенклатура = ЭП_КачествоНоменклатурыСрезПоследних.Номенклатура
	|			И ЭП_АктПриемаПередачиНоменклатура.ИнвентарныйНомер = ЭП_КачествоНоменклатурыСрезПоследних.ИнвентарныйНомер
	|ГДЕ
	|	ЭП_АктПриемаПередачиНоменклатура.Ссылка = &Ссылка
	|	И (ЭП_КачествоНоменклатурыСрезПоследних.КачествоНоменклатуры ЕСТЬ NULL
	|			ИЛИ ЭП_АктПриемаПередачиНоменклатура.КачествоНоменклатуры <> ЭП_КачествоНоменклатурыСрезПоследних.КачествоНоменклатуры)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл  
		Движение = Движения.ЭП_КачествоНоменклатуры.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура; 
		Движение.ИнвентарныйНомер = Выборка.ИнвентарныйНомер;
		Движение.КачествоНоменклатуры = Выборка.КачествоНоменклатуры;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Отвественный.Ссылка.Пустая() ИЛИ НЕ ЗначениеЗаполнено(Отвественный) Тогда
        Отвественный = ПользователиКлиентСервер.ТекущийПользователь();;
    КонецЕсли;
	
	ВидОперации = Перечисления.ЭП_ВидыОперций.ПередачаНоменклатуры;
	
КонецПроцедуры 

Процедура ПриЗаписи(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", Номенклатура.Выгрузить());
	Запрос.Текст = "ВЫБРАТЬ
	|	ТабЧасть.Номенклатура КАК Номенклатура,
	|	ТабЧасть.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ТабЧасть.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ТабЧасть.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТ_ТабЧасть
	|ИЗ
	|	&Таблица КАК ТабЧасть
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТабЧасть.Номенклатура КАК Номенклатура,
	|	ВТ_ТабЧасть.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ВТ_ТабЧасть.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ВТ_ТабЧасть.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВТ_ТабЧасть КАК ВТ_ТабЧасть
	|ГДЕ
	|	(ВТ_ТабЧасть.Номенклатура, ВТ_ТабЧасть.ИнвентарныйНомер, ВТ_ТабЧасть.ВидНоменклатуры) В
	|			(ВЫБРАТЬ
	|				ВТ.Номенклатура,
	|				ВТ.ИнвентарныйНомер,
	|				ВТ.ВидНоменклатуры
	|			ИЗ
	|				ВТ_ТабЧасть КАК ВТ
	|			СГРУППИРОВАТЬ ПО
	|				ВТ.Номенклатура,
	|				ВТ.ИнвентарныйНомер,
	|				ВТ.ВидНоменклатуры
	|			ИМЕЮЩИЕ
	|				КОЛИЧЕСТВО(ВТ.НомерСтроки) > 1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|ИТОГИ ПО
	|	Номенклатура";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выб_Номенклатура = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Выб_Номенклатура.Следующий() Цикл
			Выборка = Выб_Номенклатура.Выбрать();
			Сообщение = "";
			Пока Выборка.Следующий() Цикл
				Сообщение = Сообщение + ?(ПустаяСтрока(Сообщение), "", ", ") + Выборка.НомерСтроки;
			КонецЦикла;
			Сообщение = "Обнаружено дублирование строк: " + Сообщение;
			Сообщить(Сообщение);
		КонецЦикла;
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаПередачаНоменклатурыСотруднику()
	
	Возврат "ВЫБРАТЬ
	        |	АктПриемаПередачиНоменклатура.Номенклатура КАК Номенклатура,
	        |	АктПриемаПередачиНоменклатура.ИнвентарныйНомер КАК ИнвентарныйНомер
	        |ПОМЕСТИТЬ ВТ_Таб
	        |ИЗ
	        |	Документ.ЭП_АктПриемаПередачи.Номенклатура КАК АктПриемаПередачиНоменклатура
	        |ГДЕ
	        |	АктПриемаПередачиНоменклатура.Ссылка = &Ссылка
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	Номенклатура,
	        |	ИнвентарныйНомер
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_Таб.Номенклатура КАК Номенклатура,
	        |	ВТ_Таб.ИнвентарныйНомер КАК ИнвентарныйНомер,
	        |	ЕСТЬNULL(ЭП_ОстаткиНаСотрудникахОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокНаСотруднике,
	        |	ЕСТЬNULL(ЭП_ОстаткиНаСкладеОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокНаСкладе,
	        |	ВТ_Таб.Номенклатура.Представление КАК НоменклатураПредставление
	        |ИЗ
	        |	ВТ_Таб КАК ВТ_Таб
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЭП_ОстаткиНаСкладе.Остатки(
	        |				&МоментВремени,
	        |				(Номенклатура, ИнвентарныйНомер) В
	        |						(ВЫБРАТЬ
	        |							ВТ_Таб.Номенклатура КАК Номенклатура,
	        |							ВТ_Таб.ИнвентарныйНомер КАК ИнвентарныйНомер
	        |						ИЗ
	        |							ВТ_Таб КАК ВТ_Таб)
	        |					И Склад = &Склад) КАК ЭП_ОстаткиНаСкладеОстатки
	        |		ПО ВТ_Таб.Номенклатура = ЭП_ОстаткиНаСкладеОстатки.Номенклатура
	        |			И ВТ_Таб.ИнвентарныйНомер = ЭП_ОстаткиНаСкладеОстатки.ИнвентарныйНомер
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЭП_ОстаткиНаСотрудниках.Остатки(
	        |				&МоментВремени,
	        |				(Номенклатура, ИнвентарныйНомер) В
	        |						(ВЫБРАТЬ
	        |							ВТ_Таб.Номенклатура КАК Номенклатура,
	        |							ВТ_Таб.ИнвентарныйНомер КАК ИнвентарныйНомер
	        |						ИЗ
	        |							ВТ_Таб КАК ВТ_Таб)
	        |					И Сотрудник = &Сотрудник) КАК ЭП_ОстаткиНаСотрудникахОстатки
	        |		ПО ВТ_Таб.Номенклатура = ЭП_ОстаткиНаСотрудникахОстатки.Номенклатура
	        |			И ВТ_Таб.ИнвентарныйНомер = ЭП_ОстаткиНаСотрудникахОстатки.ИнвентарныйНомер";

КонецФункции   

Функция ТекстЗапросаПриемНоменклатурыУСотрудника()
	
	Возврат "ВЫБРАТЬ
	        |	АктПриемаПередачиНоменклатура.Номенклатура КАК Номенклатура,
	        |	АктПриемаПередачиНоменклатура.ИнвентарныйНомер КАК ИнвентарныйНомер
	        |ПОМЕСТИТЬ ВТ_Таб
	        |ИЗ
	        |	Документ.ЭП_АктПриемаПередачи.Номенклатура КАК АктПриемаПередачиНоменклатура
	        |ГДЕ
	        |	АктПриемаПередачиНоменклатура.Ссылка = &Ссылка
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	Номенклатура,
	        |	ИнвентарныйНомер
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_Таб.Номенклатура КАК Номенклатура,
	        |	ВТ_Таб.ИнвентарныйНомер КАК ИнвентарныйНомер,
	        |	ЕСТЬNULL(ЭП_ОстаткиНаСотрудникахОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокНаСотруднике,
	        |	ЕСТЬNULL(ЭП_ОстаткиНаСкладеОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокНаСкладе,
	        |	ВТ_Таб.Номенклатура.Представление КАК НоменклатураПредставление
	        |ИЗ
	        |	ВТ_Таб КАК ВТ_Таб
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЭП_ОстаткиНаСкладе.Остатки(
	        |				&МоментВремени,
	        |				(Номенклатура, ИнвентарныйНомер) В
	        |						(ВЫБРАТЬ
	        |							ВТ_Таб.Номенклатура КАК Номенклатура,
	        |							ВТ_Таб.ИнвентарныйНомер КАК ИнвентарныйНомер
	        |						ИЗ
	        |							ВТ_Таб КАК ВТ_Таб)
	        |					И Склад = &Склад) КАК ЭП_ОстаткиНаСкладеОстатки
	        |		ПО ВТ_Таб.Номенклатура = ЭП_ОстаткиНаСкладеОстатки.Номенклатура
	        |			И ВТ_Таб.ИнвентарныйНомер = ЭП_ОстаткиНаСкладеОстатки.ИнвентарныйНомер
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЭП_ОстаткиНаСотрудниках.Остатки(
	        |				&МоментВремени,
	        |				(Номенклатура, ИнвентарныйНомер) В
	        |						(ВЫБРАТЬ
	        |							ВТ_Таб.Номенклатура КАК Номенклатура,
	        |							ВТ_Таб.ИнвентарныйНомер КАК ИнвентарныйНомер
	        |						ИЗ
	        |							ВТ_Таб КАК ВТ_Таб)
	        |					И Сотрудник = &Сотрудник) КАК ЭП_ОстаткиНаСотрудникахОстатки
	        |		ПО ВТ_Таб.Номенклатура = ЭП_ОстаткиНаСотрудникахОстатки.Номенклатура
	        |			И ВТ_Таб.ИнвентарныйНомер = ЭП_ОстаткиНаСотрудникахОстатки.ИнвентарныйНомер";

КонецФункции

#КонецОбласти


