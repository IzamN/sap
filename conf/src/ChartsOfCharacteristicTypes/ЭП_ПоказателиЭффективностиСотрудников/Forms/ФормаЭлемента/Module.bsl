
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ОбновитьДлинуТочность(ТекущийОбъект.ТипЗначения);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПределыРазрядности();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		// По умолчанию баллы от 1 до 10
		ТипЗначенияДлина = 2;
		ТипЗначенияТочность = 0;
		Объект.МинЗначение = 1;
		Объект.МаксЗначение = 10;
		ОбновитьТипЗначения();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбновитьДлинуТочность(ТекущийОбъект.ТипЗначения);
КонецПроцедуры


&НаКлиенте
Процедура НаименованиеПолноеПриИзменении(Элемент)
	Объект.Наименование = Объект.НаименованиеПолное;
	Если ПустаяСтрока(Объект.Аббревиатура) Тогда
		ОбновитьАббревиатуру();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
КонецПроцедуры

&НаКлиенте
Процедура АббревиатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбновитьАббревиатуру();
КонецПроцедуры

&НаКлиенте
Процедура ТипЗначенияПриИзменении(Элемент)
	ОбновитьДлинуТочность(Объект.ТипЗначения);
КонецПроцедуры

&НаКлиенте
Процедура ТипЗначенияДлинаПриИзменении(Элемент)
	ОбновитьТипЗначения(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ТипЗначенияТочностьПриИзменении(Элемент)
	ОбновитьТипЗначения(Элемент.Имя);
КонецПроцедуры


&НаСервере
Процедура ОбновитьДлинуТочность(ТипЗначения)
	ТипЗначенияДлина = ТипЗначения.КвалификаторыЧисла.Разрядность;
	ТипЗначенияТочность = ТипЗначения.КвалификаторыЧисла.РазрядностьДробнойЧасти;
КонецПроцедуры


&НаСервере
Процедура ОбновитьТипЗначения(ИмяЭлемента = Неопределено)
	
	// Предельные данные из ПВХ
	КвалификаторыЧисла = Метаданные.ПланыВидовХарактеристик.ЭП_ПоказателиЭффективностиСотрудников.Тип.КвалификаторыЧисла;
	МаксДлинаЦелойЧасти = КвалификаторыЧисла.Разрядность - КвалификаторыЧисла.РазрядностьДробнойЧасти;
	ЗнакЧисла = КвалификаторыЧисла.ДопустимыйЗнак;
	
	// Проверить ограничения длины/точности
	Если ИмяЭлемента = "ТипЗначенияДлина" Тогда
		МинТочность = ТипЗначенияДлина - МаксДлинаЦелойЧасти;
		Если ТипЗначенияТочность < МинТочность Тогда
			ТипЗначенияТочность = МинТочность;
		КонецЕсли;
	ИначеЕсли ИмяЭлемента = "ТипЗначенияТочность" Тогда
		МаксДлина = МаксДлинаЦелойЧасти + ТипЗначенияТочность;
		Если ТипЗначенияДлина > МаксДлина Тогда
			ТипЗначенияДлина = МаксДлина;
		КонецЕсли;
	КонецЕсли;
	
	Объект.ТипЗначения = ОбщегоНазначения.ОписаниеТипаЧисло(ТипЗначенияДлина, ТипЗначенияТочность, ЗнакЧисла);
	
	Если ИмяЭлемента <> Неопределено Тогда
		Элементы.МинЗначение.ОграничениеТипа = Объект.ТипЗначения;
		Объект.МинЗначение = Объект.ТипЗначения.ПривестиЗначение(Объект.МинЗначение);
		Элементы.МаксЗначение.ОграничениеТипа = Объект.ТипЗначения;
		Объект.МаксЗначение = Объект.ТипЗначения.ПривестиЗначение(Объект.МаксЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПределыРазрядности()
	КвалификаторыЧисла = Метаданные.ПланыВидовХарактеристик.ЭП_ПоказателиЭффективностиСотрудников.Тип.КвалификаторыЧисла;
	Элементы.ТипЗначенияДлина.МаксимальноеЗначение = КвалификаторыЧисла.Разрядность;
	Элементы.ТипЗначенияТочность.МаксимальноеЗначение = КвалификаторыЧисла.РазрядностьДробнойЧасти;
КонецПроцедуры


	
&НаКлиенте
Процедура ОбновитьАббревиатуру()
	
	ИсходнаяСтрока = Объект.Наименование;
	Результат = "";
	ОжидаетсяНачалоСлова = Истина;
	Для Инд = 1 По СтрДлина(ИсходнаяСтрока) Цикл
		ТекущийСимвол = Сред(ИсходнаяСтрока, Инд, 1);
		ВидСимвола = ВидСимвола(ТекущийСимвол);

		// Включаются все цифры, заглавные буквы и первая буква каждого слова (приводится к заглавной) 
		Если ВидСимвола = "Цифра" Тогда
			Результат = Результат + ТекущийСимвол;
			ОжидаетсяНачалоСлова = Истина;
		ИначеЕсли ВидСимвола = "Буква" Тогда 
			Если Врег(ТекущийСимвол) = ТекущийСимвол
				Или ОжидаетсяНачалоСлова Тогда
				Результат = Результат + Врег(ТекущийСимвол);
			КонецЕсли;
			ОжидаетсяНачалоСлова = Ложь;
		Иначе
			ОжидаетсяНачалоСлова = Истина;
		КонецЕсли;
		
		Если СтрДлина(Результат) = 5 Тогда // Макс длина 5
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Объект.Аббревиатура = Результат;
	
КонецПроцедуры

&НаКлиенте
Функция ВидСимвола(Символ)
	
	// Взято из СтроковыеФункцииКлиентСервер.ЭтоРазделительСлов
	Диапазоны = Новый Массив;
	Диапазоны.Добавить(Новый Структура("Мин,Макс,ТипСимвола", 48, 57, "Цифра")); 		// цифры
	Диапазоны.Добавить(Новый Структура("Мин,Макс,ТипСимвола", 65, 90, "Буква")); 		// латиница большие
	Диапазоны.Добавить(Новый Структура("Мин,Макс,ТипСимвола", 97, 122, "Буква")); 		// латиница маленькие
	Диапазоны.Добавить(Новый Структура("Мин,Макс,ТипСимвола", 1040, 1103, "Буква")); 	// кириллица
	Диапазоны.Добавить(Новый Структура("Мин,Макс,ТипСимвола", 1025, 1025, "Буква")); 	// символ "Ё"
	Диапазоны.Добавить(Новый Структура("Мин,Макс,ТипСимвола", 1105, 1105, "Буква")); 	// символ "ё"
	
	КодСимвола = КодСимвола(Символ);
	Для Каждого Диапазон Из Диапазоны Цикл
		Если КодСимвола >= Диапазон.Мин И КодСимвола <= Диапазон.Макс Тогда
			Возврат Диапазон.ТипСимвола;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;

КонецФункции






