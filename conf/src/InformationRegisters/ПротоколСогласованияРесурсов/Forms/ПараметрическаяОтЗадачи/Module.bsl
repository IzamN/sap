
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбработатьПереданныеПараметры();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы
		И Модифицированность
		И Не ВыполняетсяЗакрытие Тогда
		
		Отказ = Истина;
		
		ОповещениеПослеОтветаНаВопрос = Новый ОписаниеОповещения("ОтветНаВопросЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		
		ПоказатьВопрос(ОповещениеПослеОтветаНаВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПланПриИзменении(Элемент)
	
	ИтогиПротокола = ПланСогласованоПоПротоколу(ЭтотОбъект);
	Изменение = План - ИтогиПротокола.План;
	Если Изменение <> 0 Тогда
		ИзменитьТекущиеДанныеПротокола(Изменение, ПредопределенноеЗначение("Перечисление.СтатусыСогласованияРесурса.КСогласованию"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СогласованоПриИзменении(Элемент)
	
	ИтогиПротокола = ПланСогласованоПоПротоколу(ЭтотОбъект);
	Изменение = Согласовано - ИтогиПротокола.Согласовано;
	Если Изменение <> 0 Тогда
		ИзменитьТекущиеДанныеПротокола(Изменение, ПредопределенноеЗначение("Перечисление.СтатусыСогласованияРесурса.Согласовано"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ПеренестиИзмененныеДанные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
#Область ОтрицательноеКоличество
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПротоколСогласованияКоличество.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ПротоколСогласования.Количество");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЗонаЗадачиКрасный);
	
#КонецОбласти

#Область ТекущееРедактирование

	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПротоколСогласованияПериод.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ПротоколСогласования.ЭтоТекущееРедактирование");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПротоколСогласованияДатаТекст.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ПротоколСогласования.ЭтоТекущееРедактирование");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);

#КонецОбласти

КонецПроцедуры

&НаСервере
Процедура ОбработатьПереданныеПараметры()
	
	ВидРесурса                    = Параметры.ВидРесурса;
	Согласующие                   = Параметры.Согласующие;
	ВыполняетсяСогласование       = Параметры.ВыполняетсяСогласование;
	Задача                        = Параметры.Задача;
	ТекущийПользователь           = Пользователи.ТекущийПользователь();
	СогласованоИзРодительской     = Параметры.СогласованоИзРодительской;
	ДоступноВРодительской         = Параметры.ДоступноВРодительской;
	ИсполнительРодительскойЗадачи = Параметры.ИсполнительРодительскойЗадачи;
	СогласованоПредыдущее         = Параметры.СогласованоПредыдущее;
	
	Если ВыполняетсяСогласование Тогда
	
		Если Согласующие.НайтиПоЗначению(ТекущийПользователь) <> Неопределено Тогда
			ЕстьПравоСогласованияРесурса = Истина;
		КонецЕсли;
		
		Если ТекущийПользователь = ИсполнительРодительскойЗадачи 
			И (ДоступноВРодительской > 0 
			Или СогласованоИзРодительской > 0) Тогда
			
			ЕстьПравоСогласованияИзРодительской = Истина;
			
		КонецЕсли;
		
		Если ЕстьПравоСогласованияИзРодительской
			И Не ЕстьПравоСогласованияРесурса Тогда
			
			Элементы.Согласовано.МаксимальноеЗначение = СогласованоПредыдущее + СогласованоИзРодительской + ДоступноВРодительской;
			
		КонецЕсли;
		
		ОбновитьИнформациюДоступно(ЭтотОбъект);
	
	Иначе
		
		Элементы.Согласовано.Видимость = Ложь;
		Заголовок = НСтр("ru = 'История планирования'");
		Элементы.ПротоколСогласованияСтатусСогласования.Видимость = Ложь;
		
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(Параметры.АдресПротоколаВоВременномХранилище) Тогда
		
		Если ВыполняетсяСогласование Тогда
			
			ПротоколСогласования.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресПротоколаВоВременномХранилище)); 
			
		Иначе
			
			ТаблицаСогласования = ПолучитьИзВременногоХранилища(Параметры.АдресПротоколаВоВременномХранилище);
			
			Для Каждого СтрокаТаблицы Из ТаблицаСогласования Цикл
				
				Если СтрокаТаблицы.СтатусСогласования = Перечисления.СтатусыСогласованияРесурса.КСогласованию Тогда
					
					СтрокаПротокола = ПротоколСогласования.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаПротокола, СтрокаТаблицы);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьКолонкуПериодТекст();
	УстановитьПланСогласованоПоПротоколу();
	СформироватьЗаголовокФормы();
	УправлениеДоступностью(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьЗаголовокФормы()

	ШаблонНазначения = НСтр("ru = 'Для задачи ""%1"", по ресурсу ""%2""'");
	СтрокаНазначение = СтрШаблон(ШаблонНазначения, 
	                             ?(ЗначениеЗаполнено(Параметры.Задача), Параметры.Задача, НСтр("ru = 'Новая'")),
	                             ЗадачиПроцессовПовтИсп.ПредставлениеВидаРесурса(Параметры.ВидРесурса));
	
	Элементы.ПредставлениеНазначения.Заголовок = ЗадачиПроцессов.ФорматированнаяСтрокаСГиперссылкой(СтрокаНазначение,,Ложь, ЦветаСтиля.ЦветГиперссылкиЗадачи);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеДоступностью(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.Согласовано.ТолькоПросмотр = Не Форма.ЕстьПравоСогласованияРесурса
	                                      И Не Форма.ЕстьПравоСогласованияИзРодительской;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИнформациюДоступно(Форма)
	
	ИнформацияДоступно = "";
	Если Форма.ДоступноВРодительской > 0 Тогда
		ИнформацияДоступно = СтрШаблон(НСтр("ru = 'Доступно в родительской: %1'"), Форма.ДоступноВРодительской);
	КонецЕсли;
	
	Форма.Элементы.ДекорацияДоступноИзРодительской.Заголовок = ИнформацияДоступно;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПланСогласованоПоПротоколу(Форма)
	
	План        = 0;
	Согласовано = 0;
	
	Для Каждого СтрокаПротокола Из Форма.ПротоколСогласования Цикл
		
		Если СтрокаПротокола.СтатусСогласования = ПредопределенноеЗначение("Перечисление.СтатусыСогласованияРесурса.КСогласованию") Тогда
			План = План + СтрокаПротокола.Количество;
		КонецЕсли;
		
		Если СтрокаПротокола.СтатусСогласования = ПредопределенноеЗначение("Перечисление.СтатусыСогласованияРесурса.Согласовано") Тогда
			Согласовано = Согласовано + СтрокаПротокола.Количество;
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("План",        План);
	СтруктураВозврата.Вставить("Согласовано", Согласовано);
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаСервере
Процедура УстановитьПланСогласованоПоПротоколу()
	
	ИтогиПротокола = ПланСогласованоПоПротоколу(ЭтотОбъект);
	План        = ИтогиПротокола.План;
	Согласовано = ИтогиПротокола.Согласовано;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТекущиеДанныеПротокола(Изменение, СтатусСогласования)
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("ЭтоТекущееРедактирование", Истина);
	ПараметрыПоиска.Вставить("СтатусСогласования", СтатусСогласования);
	
	НайденныеСтроки = ПротоколСогласования.НайтиСтроки(ПараметрыПоиска);
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		ИзменяемаяСтрока = ПротоколСогласования.Добавить();
		ИзменяемаяСтрока.ВидРесурса               = ВидРесурса;
		ИзменяемаяСтрока.Количество               = 0;
		ИзменяемаяСтрока.СтатусСогласования       = СтатусСогласования;
		ИзменяемаяСтрока.Автор                    = ТекущийПользователь;
		ИзменяемаяСтрока.ЭтоТекущееРедактирование = Истина;
		ИзменяемаяСтрока.ПериодТекст              = НСтр("ru = 'Сейчас'");
	Иначе
		ИзменяемаяСтрока = НайденныеСтроки[0];
	КонецЕсли;
	
	КСогласованию = План - Согласовано;
	
	Если ЕстьПравоСогласованияИзРодительской
		И СтатусСогласования = ПредопределенноеЗначение("Перечисление.СтатусыСогласованияРесурса.Согласовано")
		И (ДоступноВРодительской > 0
		Или СогласованоИзРодительской > 0) Тогда
		
		ВсегоСогласованиеИзРодительской = ДоступноВРодительской + СогласованоИзРодительской;
		
		ДоступноВРодительской     = ДоступноВРодительской - Изменение;
		СогласованоИзРодительской = СогласованоИзРодительской + Изменение;

		Если ДоступноВРодительской < 0 Тогда
			ДоступноВРодительской = 0;
			СогласованоИзРодительской = ВсегоСогласованиеИзРодительской;
		ИначеЕсли ДоступноВРодительской > ВсегоСогласованиеИзРодительской Тогда
			ДоступноВРодительской     = ВсегоСогласованиеИзРодительской;
			СогласованоИзРодительской = 0;
		КонецЕсли;
		
		Если КСогласованию < 0 Тогда
			
			КУменьшениюСогласовано = Мин(- КСогласованию, СогласованоИзРодительской);
			
			Если КУменьшениюСогласовано > 0 Тогда
			
				Согласовано               = Согласовано - КУменьшениюСогласовано;
				СогласованоИзРодительской = СогласованоИзРодительской - КУменьшениюСогласовано;
				ДоступноВРодительской     = ДоступноВРодительской + КУменьшениюСогласовано;
				Изменение                 = Изменение - КУменьшениюСогласовано;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтатусСогласования = ПредопределенноеЗначение("Перечисление.СтатусыСогласованияРесурса.КСогласованию") Тогда
		
		Если КСогласованию < 0 
			И СогласованоИзРодительской > 0 Тогда
			
			КУменьшениюСогласовано    = Мин(- КСогласованию, СогласованоИзРодительской);
			
			Если КУменьшениюСогласовано > 0 Тогда
				
				Согласовано               = Согласовано - КУменьшениюСогласовано;
				СогласованоИзРодительской = СогласованоИзРодительской - КУменьшениюСогласовано;
				ДоступноВРодительской     = ДоступноВРодительской + КУменьшениюСогласовано;
				
				ПараметрыПоиска = Новый Структура;
				ПараметрыПоиска.Вставить("ЭтоТекущееРедактирование", Истина);
				ПараметрыПоиска.Вставить("СтатусСогласования", ПредопределенноеЗначение("Перечисление.СтатусыСогласованияРесурса.Согласовано"));
				
				НайденныеСтроки = ПротоколСогласования.НайтиСтроки(ПараметрыПоиска);
				
				Если НайденныеСтроки.Количество() > 0 Тогда
					НайденныеСтроки[0].Количество = НайденныеСтроки[0].Количество - КУменьшениюСогласовано;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ИзменяемаяСтрока.Количество = ИзменяемаяСтрока.Количество + Изменение;
	
	Если ИзменяемаяСтрока.Количество = 0 Тогда 
		ПротоколСогласования.Удалить(ИзменяемаяСтрока);
	КонецЕсли;
	
	ОбновитьИнформациюДоступно(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКолонкуПериодТекст()

	Для Каждого СтрокаПротокола Из ПротоколСогласования Цикл
		
		Если СтрокаПротокола.ЭтоТекущееРедактирование Тогда
			СтрокаПротокола.ПериодТекст = НСтр("ru = 'Сейчас'");
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Функция ПодготовленнаяСтруктураВозврата()
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Задача",                    Задача);
	СтруктураВозврата.Вставить("ВидРесурса",                ВидРесурса);
	СтруктураВозврата.Вставить("План",                      План);
	СтруктураВозврата.Вставить("Согласовано",               Согласовано);
	СтруктураВозврата.Вставить("ДоступноВРодительской",     ДоступноВРодительской);
	СтруктураВозврата.Вставить("СогласованоИзРодительской", СогласованоИзРодительской);
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаКлиенте
Процедура ОтветНаВопросЗавершение(ОтветНаВопрос, ДополнительныеПараметры) Экспорт

	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		
		ПеренестиИзмененныеДанные();
		
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		
		ВыполняетсяЗакрытие = Истина;
		Закрыть(Неопределено);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПеренестиИзмененныеДанные()

	ВыполняетсяЗакрытие = Истина;
	
	Если Модифицированность Тогда
		Закрыть(ПодготовленнаяСтруктураВозврата());
	Иначе
		Закрыть(Неопределено);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
