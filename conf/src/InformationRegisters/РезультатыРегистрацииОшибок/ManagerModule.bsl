
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Тест.Владелец)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиОбновления

Процедура ЗарегистрироватьДанныеКОбработкеДляЗаполненияПроекта(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РезультатыРегистрацииОшибок.Ветка КАК Ветка
	|ИЗ
	|	РегистрСведений.РезультатыРегистрацииОшибок КАК РезультатыРегистрацииОшибок
	|ГДЕ
	|	РезультатыРегистрацииОшибок.Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)";
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.РезультатыРегистрацииОшибок";
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Результат, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляЗаполненияПроекта(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.РезультатыРегистрацииОшибок;
	ПолноеИмяОбъекта  = МетаданныеОбъекта.ПолноеИмя();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметры.ИмяИзмеренияДляОтбора = "Ветка";
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;																									
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		ПолноеИмяОбъекта,
		МенеджерВременныхТаблиц,
		ДополнительныеПараметры);
																									
																									
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДляОбработки.Ветка КАК Ветка,
	|	ДанныеДляОбработки.Ветка.Владелец КАК Проект
	|ИЗ
	|	ИмяВременнойТаблицы КАК ДанныеДляОбработки";
	
    Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяВременнойТаблицы", Результат.ИмяВременнойТаблицы);
	Выборка = Запрос.Выполнить().Выбрать();
																									
	ЗаписейОбработано = 0;
	ПроблемныхЗаписей = 0;
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировкиДанных.УстановитьЗначение("Ветка", Выборка.Ветка);
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.РезультатыРегистрацииОшибок.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Ветка.Установить(Выборка.Ветка);
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() = 0 Тогда
				
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
				
			Иначе
				
				Для Каждого ТекЗапись Из НаборЗаписей Цикл
					Если НЕ ЗначениеЗаполнено(ТекЗапись.Проект) Тогда
						ТекЗапись.Проект = Выборка.Проект;
					КонецЕсли;	
				КонецЦикла;
				
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
				
			КонецЕсли;
			
			ЗаписейОбработано = ЗаписейОбработано + 1;
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ПроблемныхЗаписей = ПроблемныхЗаписей + 1;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать запись с идентификатором: %1 по причине:
					|%2'"), 
					НаборЗаписей, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.РезультатыРегистрацииОшибок, , ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	Если ПроблемныхЗаписей <> 0 Тогда
		ТекстСообщения = НСтр("ru = 'Процедура ОбработатьДанныеДляЗаполненияПроекта завершилась с ошибкой. Не все записи удалось обновить.'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
