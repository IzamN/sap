#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ЗадачиДляВыгрузки.Видимость = Ложь;  
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы 

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	
	ПроектПриИзмененииНаСервере();
	
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерезаполнитьСписокЗначений(Команда)
	
	ПерезаполнитьСписокЗначенийНаСервере(); 
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(ПараметрыВыполнения) 
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Каталог = "";
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Заголовок = "Выберите каталог"; 
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("ПриВыбореКаталога", ЭтотОбъект);
	
	Диалог.Показать(ОповещениеЗавершения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует массив структур с данными файлов, которые необходимо выгрузить
&НаСервере
Функция ВыгрузитьНаСервере()
	
	МассивПутейКФайлам = Новый Массив;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если СтатусыЗадач.Количество() > 0 Тогда
		
		// Проверка того, что есть необходимые свойства по проекту, чтобы сделать отбор по статусам задач
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство.Ссылка КАК СвойствоСсылка
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаборыДополнительныхРеквизитовИСведений КАК НаборыДополнительныхРеквизитовИСведений
		|		ПО НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = НаборыДополнительныхРеквизитовИСведений.Ссылка
		|ГДЕ
		|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство.Заголовок ПОДОБНО ""Статус%""
		|	И НаборыДополнительныхРеквизитовИСведений.Наименование = &Наименование";
		
		Запрос.УстановитьПараметр("Наименование", Строка(Проект));
		
		РезультатЗапроса = Запрос.Выполнить();  
		
		Если Не РезультатЗапроса.Пустой() Тогда  
			
			Выборка = РезультатЗапроса.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				Свойство = Выборка.СвойствоСсылка;	
				
			КонецЦикла;
		Иначе
			Свойство = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаФайлыКВыгрузке(); 
	
	Запрос.УстановитьПараметр("Проект", Проект);
	Запрос.УстановитьПараметр("Значение", СтатусыЗадач.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("РазделПроекта", ЭтапПроекта);
	Запрос.УстановитьПараметр("Свойство", Свойство);
	Запрос.УстановитьПараметр("ТехническийПроект", ТехническийПроект.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("ПроверкаТехПроекта",НЕ ТехническийПроект.Количество() > 0);
	Запрос.УстановитьПараметр("ПроверкаСовйства", НЕ ЗначениеЗаполнено(Свойство));
	Запрос.УстановитьПараметр("ПроверкаСтатусовЗадач", НЕ СтатусыЗадач.Количество() > 0);
	Запрос.УстановитьПараметр("ПровекаСтатусаТехПроекта", НЕ СтатусыТехПроектов.Количество() > 0);
	Запрос.УстановитьПараметр("Статус", СтатусыТехПроектов.ВыгрузитьЗначения());
		
	Выборка = Запрос.Выполнить().Выбрать();  
	
	Пока Выборка.Следующий() Цикл  
		МассивПутейКФайламСтруктура = Новый Структура("Имя", "Хранение", "ТехПроект");
		МассивПутейКФайламСтруктура.Вставить("Хранение", Выборка.Ссылка);
		МассивПутейКФайламСтруктура.Вставить("Имя", Выборка.ПутьКФайлу);
		МассивПутейКФайламСтруктура.Вставить("ТехПроект", Выборка.ТехПроект);
		МассивПутейКФайлам.Добавить(МассивПутейКФайламСтруктура);
	КонецЦикла;
	
	Возврат МассивПутейКФайлам;
	
	УстановитьПривилегированныйРежим(Ложь);

КонецФункции

// Формирует масссив структур с сылками на файлы, которые необходимо скачать
&НаСервере
Функция ПодготовитьФайлыНаСервере()
	
	ИмяФайловДляСкачивания = Новый Массив;
	
	ИмяФайлов = ВыгрузитьНаСервере();
	
	Для Каждого ИмяФайла Из ИмяФайлов Цикл  
		// Пытается получить ссылку на двоичные данные файла, без использования попытки уходит в ошибку
		Попытка 		
			ИмяФайловДляСкачиванияСтруктура = Новый Структура("Имя, Хранение", "ТехПроект");
			Файл = РаботаСФайлами.ДанныеФайла(ИмяФайла.Хранение,);
			ИмяФайловДляСкачиванияСтруктура.Вставить("Имя", ИмяФайла.Имя);
			ИмяФайловДляСкачиванияСтруктура.Вставить("Хранение", Файл.СсылкаНаДвоичныеДанныеФайла); 
			ИмяФайловДляСкачиванияСтруктура.Вставить("ТехПроект", ИмяФайла.ТехПроект);
			ИмяФайловДляСкачивания.Добавить(ИмяФайловДляСкачиванияСтруктура);			
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось обнаружить файл " + ИмяФайла.Имя,,,,);
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат ИмяФайловДляСкачивания;
	
КонецФункции

// Создает каталоги файлов на клиенте по техническим проектам.
//
// Параметры:
//  ИмяФайловДляСкачивания - Массив структур - массив в который будут добавлены структуры типа 
//  ("Имя, Хранение", "ТехПроект"), где Имя - строка, путь к файлу на сервере, Хранение - ссылка на файл для скачивания,
//	ТехПроект - СправочникСсылка.ТехПроекты, ссылка на технический проект для контроля заполнения папок
//  ИмяПапкиТехПроекта - строка - путь к папке технического проекта 
//  ТехПроект - СправочникиСсылка.ТехническийПроект - ссылка на технический проект, для сравнения с элементом структуры
// 	ТехПроект
//
&НаКлиенте
Процедура Получить(ИмяФайловДляСкачивания, ИмяПапкиТехПроекта, ТехПроект) 
	
	Для Каждого ИмяФайла Из ИмяФайловДляСкачивания Цикл
		
		Если ИмяФайла.ТехПроект = ТехПроект Тогда
			
			Название = ИмяПапкиТехПроекта;
			Название = СтрЗаменить(Название," ","_");
			ПутьКФайлу = ИмяФайла.Имя;
			НомерРазделителя = СтрНайти(ИмяФайла.Имя,"\",,,); 
			Файл = Лев(ПутьКФайлу,НомерРазделителя - 1);
			ПутьКФайлу = СтрЗаменить(ПутьКФайлу,Файл,"");
			ПутьКФайлу = СтрЗаменить(ПутьКФайлу,"\","");
			СоздатьКаталог(Название); 
			НачатьПолучениеФайлаССервера(,ИмяФайла.Хранение, Название + ПолучитьРазделительПути() + ПутьКФайлу); 
			
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры   

&НаКлиенте
Процедура ПриВыбореКаталога(ВыбКаталог, ДополнительныеПараметры) Экспорт

	Если ВыбКаталог <> Неопределено Тогда
		
		ПапкаЗагрузки = ВыбКаталог[0];
		
		ИмяФайловДляСкачивания = ПодготовитьФайлыНаСервере();
		
		Если ИмяФайловДляСкачивания.Количество() = 0 Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю("Нет данных для выгрузки"); 
			Возврат;
		Иначе
			Кавычки = """";
			ИмяФайлаКаталога = СтрШаблон(ПапкаЗагрузки + ПолучитьРазделительПути() + "Выгрузка_данных_по %1 по этапу %2 от %3", 
			СтрЗаменить(Строка(Проект),Кавычки,""),ЭтапПроекта, Формат(ТекущаяДата(),"ДФ=yyyy-MM-dd-hh-mm; ДЛФ=DT"));
			СоздатьКаталог(ИмяФайлаКаталога); 
			
			//Цикл, который формирует папки по техическим проектам в выбранном пользователем каталоге, заполняет каждую папку
			// файлами по техническим проектам
			
			Для Каждого ИмяФайла Из ИмяФайловДляСкачивания Цикл
				
				Если ЗначениеЗаполнено(ИмяФайла.Имя) И ЗначениеЗаполнено(ИмяФайла.Хранение)  И ЗначениеЗаполнено(ИмяФайла.ТехПроект) Тогда 
					
					ИмяПапкиТехПроекта = ИмяФайлаКаталога + ПолучитьРазделительПути() + СтрЗаменить(Строка(ИмяФайла.ТехПроект),Кавычки,"");				
					СоздатьКаталог(ИмяПапкиТехПроекта);				
					Получить(ИмяФайловДляСкачивания, ИмяПапкиТехПроекта, ИмяФайла.ТехПроект);
					
				КонецЕсли;
				
			КонецЦикла;
			
			ОбщегоНазначенияКлиент.СообщитьПользователю("Выгрузка данных успешно завершена"); 
			
		КонецЕсли;	
		
	КонецЕсли;
   
КонецПроцедуры

//Заполняет список выбора значениями ДопРеквезитов по статусам задач в зависимости от выбранного проекта
&НаСервере
Процедура ПроектПриИзмененииНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НаборыДополнительныхРеквизитовИСведений.Представление КАК Представление,
		|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство.Заголовок КАК СвойствоЗаголовок,
		|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство.Ссылка КАК СвойствоСсылка
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаборыДополнительныхРеквизитовИСведений КАК НаборыДополнительныхРеквизитовИСведений
		|		ПО НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = НаборыДополнительныхРеквизитовИСведений.Ссылка
		|ГДЕ
		|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство.Заголовок ПОДОБНО ""Статус%""
		|	И НаборыДополнительныхРеквизитовИСведений.Наименование = &Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗначенияСвойствОбъектов.Ссылка КАК Статусы,
		|	ЗначенияСвойствОбъектов.Представление КАК Представление
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|		ПО ВТ.СвойствоСсылка = ЗначенияСвойствОбъектов.Владелец";
	
	Запрос.УстановитьПараметр("Наименование", Строка(Проект));	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда 
		
		Элементы.ЗадачиДляВыгрузки.Видимость = Истина; 
		СтатусыЗадач.Очистить(); 
		СтатусыЗадачКопия.Очистить(); 
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СтатусыЗадач.Добавить(Выборка.Статусы,Выборка.Представление); 
			СтатусыЗадачКопия.Добавить(Выборка.Статусы,Выборка.Представление);
		КонецЦикла; 
				
	Иначе   
		
		Элементы.ЗадачиДляВыгрузки.Видимость = Ложь;
		СтатусыЗадач.Очистить(); 
		СтатусыЗадачКопия.Очистить();
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьСписокЗначенийНаСервере()

	СтатусыЗадач.Очистить();
	СтатусыЗадач = СтатусыЗадачКопия;
	
КонецПроцедуры

Функция ПолучитьТекстЗапросаФайлыКВыгрузке()
	
	Возврат 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТехническиеПроекты.Ссылка КАК ТехПроект
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	Справочник.ТехническиеПроекты КАК ТехническиеПроекты
	|ГДЕ
	|	(&ПроверкаТехПроекта
	|			ИЛИ ТехническиеПроекты.Ссылка В (&ТехническийПроект))
	|	И ТехническиеПроекты.Владелец = &Проект
	|	И ТехническиеПроекты.РазделПроекта = &РазделПроекта
	|	И (&ПровекаСтатусаТехПроекта
	|			ИЛИ ТехническиеПроекты.Статус В (&Статус))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТехПроект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиПроцесса.Ссылка КАК ЗадачаПроцесса,
	|	ВТ.ТехПроект КАК ТехПроект
	|ПОМЕСТИТЬ ВТ_ТехПроекты_И_ЗадачиПроцесса_По_Условиям
	|ИЗ
	|	Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса
	|		ПРАВОЕ СОЕДИНЕНИЕ ВТ КАК ВТ
	|		ПО (ВТ.ТехПроект = ЗадачиПроцесса.Предмет)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗадачаПроцесса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТехПроекты_И_ЗадачиПроцесса_По_Условиям.ЗадачаПроцесса КАК ЗадачаПроцесса,
	|	ВТ_ТехПроекты_И_ЗадачиПроцесса_По_Условиям.ТехПроект КАК ТехПроект
	|ПОМЕСТИТЬ ВТ_ЗадачиПоУсловиям
	|ИЗ
	|	ВТ_ТехПроекты_И_ЗадачиПроцесса_По_Условиям КАК ВТ_ТехПроекты_И_ЗадачиПроцесса_По_Условиям
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса.ДополнительныеРеквизиты КАК ЗадачиПроцессаДополнительныеРеквизиты
	|		ПО ВТ_ТехПроекты_И_ЗадачиПроцесса_По_Условиям.ЗадачаПроцесса = ЗадачиПроцессаДополнительныеРеквизиты.Ссылка
	|ГДЕ
	|	(&ПроверкаСовйства
	|			ИЛИ ЗадачиПроцессаДополнительныеРеквизиты.Свойство = &Свойство)
	|	И (&ПроверкаСтатусовЗадач
	|			ИЛИ ЗадачиПроцессаДополнительныеРеквизиты.Значение В (&Значение))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТехПроект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗадачиПоУсловиям.ТехПроект КАК ТехПроект
	|ПОМЕСТИТЬ ВТ_ПодходящиеТехПроекты
	|ИЗ
	|	ВТ_ЗадачиПоУсловиям КАК ВТ_ЗадачиПоУсловиям
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ЗадачиПоУсловиям.ТехПроект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТехПроект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Файлы.Ссылка КАК Ссылка,
	|	Файлы.ПутьКФайлу КАК ПутьКФайлу,
	|	ВТ_ПодходящиеТехПроекты.ТехПроект КАК ТехПроект
	|ИЗ
	|	ВТ_ПодходящиеТехПроекты КАК ВТ_ПодходящиеТехПроекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Файлы КАК Файлы
	|		ПО ВТ_ПодходящиеТехПроекты.ТехПроект = Файлы.ВладелецФайла
	|ГДЕ
	|	Файлы.ВладелецФайла ЕСТЬ НЕ NULL ";	
	
КонецФункции 

#КонецОбласти
