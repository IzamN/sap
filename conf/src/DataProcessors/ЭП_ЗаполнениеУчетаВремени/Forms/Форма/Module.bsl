
&НаКлиенте
Процедура СгенерироватьЗаписи(Команда)
	СгенерироватьЗаписиНаСервере();
КонецПроцедуры

&НаСервере
Процедура СгенерироватьЗаписиНаСервере()
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЭП_ЭффективностьСотрудников.Сотрудник КАК Сотрудник,
	               |	ЭП_ЭффективностьСотрудников.Значение КАК Значение
	               |ПОМЕСТИТЬ ВТ_ТекущиеЗначения
	               |ИЗ
	               |	РегистрСведений.ЭП_ЭффективностьСотрудников КАК ЭП_ЭффективностьСотрудников
	               |ГДЕ
	               |	ЭП_ЭффективностьСотрудников.ПериодОценивания = &ПериодОценивания
	               |	И ЭП_ЭффективностьСотрудников.Показатель = &Показатель
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Сотрудник
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&ПериодОценивания КАК ПериодОценивания,
	               |	&Ответственный КАК Ответственный,
	               |	УчетВремениОбороты.Проект КАК Проект,
	               |	УчетВремениОбороты.Пользователь.ЭП_Сотрудник КАК Сотрудник,
	               |	ЭП_ПрофилиРасчетаРезультативности.Ссылка КАК ПрофильРасчета,
	               |	&Показатель КАК Показатель,
	               |	УчетВремениОбороты.ДлительностьОборот / 3600 КАК Значение
	               |ИЗ
	               |	РегистрНакопления.УчетВремени.Обороты(НАЧАЛОПЕРИОДА(&ПериодОценивания, МЕСЯЦ), ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(&ПериодОценивания, МЕСЯЦ), ДЕНЬ, 5), , ) КАК УчетВремениОбороты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭП_ПрофилиРасчетаРезультативности КАК ЭП_ПрофилиРасчетаРезультативности
	               |		ПО УчетВремениОбороты.ВидДеятельности = ЭП_ПрофилиРасчетаРезультативности.ВидДеятельности
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТекущиеЗначения КАК ВТ_ТекущиеЗначения
	               |		ПО УчетВремениОбороты.Пользователь.ЭП_Сотрудник = ВТ_ТекущиеЗначения.Сотрудник
	               |			И (НЕ УчетВремениОбороты.ДлительностьОборот = ВТ_ТекущиеЗначения.Значение)
	               |ГДЕ
	               |	УчетВремениОбороты.Пользователь.ЭП_ГенерацияЗаписейВремени
	               |	И ВТ_ТекущиеЗначения.Сотрудник ЕСТЬ NULL"; 
	Запрос.УстановитьПараметр("ПериодОценивания",НачалоМесяца(ПериодОценивания));
	Запрос.УстановитьПараметр("Ответственный", ТекущийПользователь);
	Запрос.УстановитьПараметр("Показатель", Показатель); 
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "За выбранный период не найдено внесенных часов.";        
		Сообщение.Сообщить();
		Возврат;		
	
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	НаборДанных = РегистрыСведений.ЭП_ЭффективностьСотрудников.СоздатьМенеджерЗаписи();
	
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(НаборДанных, Выборка); 
		НаборДанных.Комментарий = "Запись созданна автоматической обработкой.";
		НаборДанных.Записать();
	
	КонецЦикла;
	
КонецПроцедуры
