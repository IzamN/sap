#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПериодОценивания = НачалоМесяца(ТекущаяДатаСеанса());
	
КонецПроцедуры         

&НаКлиенте
Процедура ПериодОцениванияПриИзменении(Элемент)
	
	ПериодОценивания = НачалоМесяца(ПериодОценивания);
	
	Если Модифицированность Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриИзмененииНастроекВводаПродолжение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, "Сохранить изменения показателей?", РежимДиалогаВопрос.ДаНетОтмена);
		Возврат;
			
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Показатель) Тогда
	
		ЗаполнитьПроекты();	
	
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательПриИзменении(Элемент)
	
	Если Модифицированность Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриИзмененииНастроекВводаПродолжение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, "Сохранить изменения показателей?", РежимДиалогаВопрос.ДаНетОтмена);
		Возврат;
			
	КонецЕсли;
	
	ЗаполнитьПроекты();	

КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура Записать(Команда)
	ЗаписатьЗначенияПоказателя();
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	ЗаписатьЗначенияПоказателя();
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область Служебные

&НаСервере
Процедура ЗаполнитьПроекты()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЭП_КоманднаяРаботаПоПроекту.Проект КАК Проект,
	               |	ЭП_КоманднаяРаботаПоПроекту.Показатель КАК Показатель,
	               |	ЭП_КоманднаяРаботаПоПроекту.Значение КАК Значение,
	               |	ЭП_КоманднаяРаботаПоПроекту.ПериодОценивания КАК ПериодОценивания
	               |ПОМЕСТИТЬ ВТ_ЗаполненныеПоказателиПоПрошломуПериоду
	               |ИЗ
	               |	РегистрСведений.ЭП_КоманднаяРаботаПоПроекту КАК ЭП_КоманднаяРаботаПоПроекту
	               |ГДЕ
	               |	ЭП_КоманднаяРаботаПоПроекту.ПериодОценивания = &ПериодОценивания
	               |	И ЭП_КоманднаяРаботаПоПроекту.Показатель = &Показатель
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	УчетВремениОбороты.Проект КАК Проект,
	               |	&ПериодОценивания КАК ПериодОценивания,
	               |	&Показатель КАК Показатель,
	               |	ЕСТЬNULL(ВТ_ЗаполненныеПоказателиПоПрошломуПериоду.Значение, 0) КАК Значение
	               |ИЗ
	               |	РегистрНакопления.УчетВремени.Обороты(&ПериодОценивания, КОНЕЦПЕРИОДА(&ПериодОценивания, МЕСЯЦ), , ) КАК УчетВремениОбороты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
	               |		ПО УчетВремениОбороты.Проект = Проекты.Ссылка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗаполненныеПоказателиПоПрошломуПериоду КАК ВТ_ЗаполненныеПоказателиПоПрошломуПериоду
	               |		ПО УчетВремениОбороты.Проект = ВТ_ЗаполненныеПоказателиПоПрошломуПериоду.Проект
	               |ГДЕ
	               |	НЕ Проекты.ПометкаУдаления";
	Запрос.УстановитьПараметр("ПериодОценивания", ПериодОценивания);
	Запрос.УстановитьПараметр("Показатель", Показатель);
	Выборка = Запрос.Выполнить().Выбрать();
	
	АктуальныеПроекты.Очистить();
	
	Пока Выборка.Следующий() Цикл
	
		НовСтр = АктуальныеПроекты.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр,Выборка);
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииНастроекВводаПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ЗаписатьЗначенияПоказателя();
		
		Если ЗначениеЗаполнено(Показатель) Тогда
	
			ЗаполнитьПроекты();	
	
		КонецЕсли; 
	
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		Если ЗначениеЗаполнено(Показатель) Тогда
	
			ЗаполнитьПроекты();	
	
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЗначенияПоказателя()
	
	Для каждого СтрокаТаблицы Из АктуальныеПроекты Цикл
		
		//Если СтрокаТаблицы.Значение = 0 Тогда
		//
		//	Продолжить;
		//
		//КонецЕсли;
		МенеджерЗаписиРегистра = РегистрыСведений.ЭП_КоманднаяРаботаПоПроекту.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписиРегистра, СтрокаТаблицы);
		МенеджерЗаписиРегистра.Записать(Истина);
	
	КонецЦикла;  
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектыПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоВсемПроектам(Команда)
	ПоказатьВводЧисла(Новый ОписаниеОповещения("ВводЧислаЗавершение", ЭтотОбъект)
					  ,0
					  ,"Введите значение показателя для всех проектов:"
					  , 4
					  , 2); 
КонецПроцедуры

&НаКлиенте
Процедура ВводЧислаЗавершение(Число, ДополнительныеПараметры) Экспорт

	Если Число = Неопределено Тогда
	
		Возврат;		
	
	КонецЕсли;
	
	Для каждого СтрокаТЧ Из АктуальныеПроекты Цикл
		
		Если ЗначениеЗаполнено(СтрокаТЧ.Значение) Тогда
		
			Продолжить;			
		
		КонецЕсли;
		СтрокаТЧ.Значение = Число;		
		Модифицированность = Истина;	
	КонецЦикла;

КонецПроцедуры

#КонецОбласти
