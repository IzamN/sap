#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("Владелец") Тогда
		Возврат;
	КонецЕсли;
	
	СборкаОбнаружения = Неопределено;
	ВеткаОбнаружения = Неопределено;
	
	ОтборПоСборке = Параметры.Свойство("СборкаОбнаружения", СборкаОбнаружения);
	ОтборПоВетке  = Параметры.Свойство("ВеткаОбнаружения",  ВеткаОбнаружения);
	
	Если НЕ ОтборПоСборке и НЕ ОтборПоВетке Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
		
	Если ЗначениеЗаполнено(СборкаОбнаружения) Тогда
		Версия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СборкаОбнаружения, "Владелец");
	ИначеЕсли ЗначениеЗаполнено(ВеткаОбнаружения) Тогда
		Версия = Справочники.Ошибки.ВерсияОбнаруженияДляВетки(ВеткаОбнаружения);
	Иначе
		Версия = Справочники.ВерсииПроекта.ПустаяСсылка();
	КонецЕсли;
		
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ПроектыПорядокВоспроизведенияИИсправленияОшибок.ВерсияИсправленияНемедленно КАК ВерсияИсправленияНемедленно,
	|	ПроектыПорядокВоспроизведенияИИсправленияОшибок.ВерсияИсправленияЖелательноБыстрее КАК ВерсияИсправленияЖелательноБыстрее,
	|	ПроектыПорядокВоспроизведенияИИсправленияОшибок.ВерсияИсправленияВПлановомПорядке КАК ВерсияИсправленияВПлановомПорядке
	|ИЗ
	|	Справочник.Проекты.ПорядокВоспроизведенияИИсправленияОшибок КАК ПроектыПорядокВоспроизведенияИИсправленияОшибок
	|ГДЕ
	|	ПроектыПорядокВоспроизведенияИИсправленияОшибок.ВерсияВоспроизведения = &ВерсияОбнаружения
	| И (Не &ОтбиратьПоСтрокеПоиска 
	|	ИЛИ ""Немедленно"" ПОДОБНО &СтрокаПоиска
	|	ИЛИ ""Желательно быстрее"" ПОДОБНО &СтрокаПоиска
	|	ИЛИ ""В плановом порядке"" ПОДОБНО &СтрокаПоиска)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПроектыПорядокВоспроизведенияИИсправленияОшибок.ВерсияВоспроизведения"
	;
	
	ВерсияНемедленно = Неопределено;
	ВерсияЖелательноБыстрее = Неопределено;
	ВерсияВПлановомПорядке = Неопределено;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВерсияОбнаружения", Версия);
	
	СтрокаПоиска = Параметры.СтрокаПоиска;
	
	Если СтрокаПоиска <> Неопределено Тогда
		Запрос.УстановитьПараметр("СтрокаПоиска", СтрокаПоиска + "%");
		Запрос.УстановитьПараметр("ОтбиратьПоСтрокеПоиска", Истина);
	Иначе
		Запрос.УстановитьПараметр("СтрокаПоиска", "");
		Запрос.УстановитьПараметр("ОтбиратьПоСтрокеПоиска", Ложь);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			
			Если ЗначениеЗаполнено(Выборка.ВерсияИсправленияНемедленно) Тогда
				ВерсияНемедленно = Выборка.ВерсияИсправленияНемедленно;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.ВерсияИсправленияЖелательноБыстрее) Тогда
				ВерсияЖелательноБыстрее = Выборка.ВерсияИсправленияЖелательноБыстрее;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.ВерсияИсправленияВПлановомПорядке) Тогда
				ВерсияВПлановомПорядке = Выборка.ВерсияИсправленияВПлановомПорядке;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Если ВерсияНемедленно = Неопределено
		И ВерсияЖелательноБыстрее = Неопределено
		И ВерсияВПлановомПорядке = Неопределено
		И СтрокаПоиска <> Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПредставлениеНемедленно = 
		ПредставлениеСрочности(Перечисления.СрочностьИсправленияОшибок.Немедленно, ВерсияНемедленно);
	
	ПредставлениеЖелательноБыстрее = 
		ПредставлениеСрочности(Перечисления.СрочностьИсправленияОшибок.ЖелательноБыстрее, ВерсияЖелательноБыстрее);
	
	ПредставлениеВПлановомПорядке = 
		ПредставлениеСрочности(Перечисления.СрочностьИсправленияОшибок.ВПлановомПорядке, ВерсияВПлановомПорядке);
	
	ДанныеВыбора.Добавить(Перечисления.СрочностьИсправленияОшибок.Немедленно, ПредставлениеНемедленно);
	ДанныеВыбора.Добавить(Перечисления.СрочностьИсправленияОшибок.ЖелательноБыстрее, ПредставлениеЖелательноБыстрее);
	ДанныеВыбора.Добавить(Перечисления.СрочностьИсправленияОшибок.ВПлановомПорядке, ПредставлениеВПлановомПорядке);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеСрочности(ЗначениеСрочности, Версия)

	Если ЗначениеЗаполнено(Версия) Тогда
		Представление = Строка(ЗначениеСрочности) + " (" + Версия +  ")";
	Иначе
		Представление = Строка(ЗначениеСрочности);
	КонецЕсли;
	
	Возврат Представление;
		
КонецФункции
	
#КонецОбласти

#КонецЕсли