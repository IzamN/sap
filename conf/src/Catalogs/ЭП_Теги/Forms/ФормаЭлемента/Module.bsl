
#Область ОбработчикиСобытийФормы  

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Проект.Пустая() Тогда
		Объект.Владелец = Параметры.Проект;
		Объект.Наименование = Параметры.ТекстЗаполнения;
	КонецЕсли; 
		
	Если Параметры.ИзменениеЦветНазвание Тогда
		Элементы.Владелец.Видимость = Ложь;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда 
		//Установка редко используемого цвета
		ДанныеЦвета = ПолучениеСлучайногоЦвета(Объект.Владелец);                    
		Объект.НазваниеЦвета = ДанныеЦвета.НазваниеЦвета;
		Элементы.ПредставлениеЦвета.ЦветФона = ДанныеЦвета.Цвет;
	Иначе 
		Цвет = ПолучениеЦвета(Объект.НазваниеЦвета); 
		Элементы.ПредставлениеЦвета.ЦветФона = Новый Цвет(Цвет.Красный, Цвет.Зеленый, Цвет.Синий);
	КонецЕсли; 
	
КонецПроцедуры  

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	СтруктураЦвет = ПолучениеЦвета(Объект.НазваниеЦвета);
	Цвет = Строка(СтруктураЦвет.Красный) + ", " + СтруктураЦвет.Зеленый + ", " + СтруктураЦвет.Синий;
	ПараметрыФормы = Новый Структура("Тег, Представление, Цвет", Объект.Ссылка, Объект.Наименование, Цвет);
	Оповестить("Справочник.ЭП_Теги.Изменение", ПараметрыФормы, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовГруппаЦвет

&НаКлиенте
Процедура НазваниеЦветаПриИзменении(Элемент)
	Цвет = ПолучениеЦвета(Объект.НазваниеЦвета); 
	Элементы.ПредставлениеЦвета.ЦветФона = Новый Цвет(Цвет.Красный, Цвет.Зеленый, Цвет.Синий);
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучениеСлучайногоЦвета(Проект)
	
	Запрос = Новый Запрос; 
	Запрос.УстановитьПараметр("Проект", Проект);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭП_Теги.Ссылка КАК Ссылка,
	|	ЭП_Теги.НазваниеЦвета КАК НазваниеЦвета
	|ПОМЕСТИТЬ ВТ_Теги
	|ИЗ
	|	Справочник.ЭП_Теги КАК ЭП_Теги
	|ГДЕ
	|	ЭП_Теги.Владелец = &Проект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НазваниеЦвета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	МАКСИМУМ(ЭП_КлассификаторЦветов.Ссылка) КАК НазваниеЦвета,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_Теги.Ссылка) КАК Тег,
	|	ЭП_КлассификаторЦветов.Красный КАК Красный,
	|	ЭП_КлассификаторЦветов.Зеленый КАК Зеленый,
	|	ЭП_КлассификаторЦветов.Синий КАК Синий
	|ИЗ
	|	Справочник.ЭП_КлассификаторЦветов КАК ЭП_КлассификаторЦветов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Теги КАК ВТ_Теги
	|		ПО ЭП_КлассификаторЦветов.Ссылка = ВТ_Теги.НазваниеЦвета
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭП_КлассификаторЦветов.Красный,
	|	ЭП_КлассификаторЦветов.Зеленый,
	|	ЭП_КлассификаторЦветов.Синий
	|
	|УПОРЯДОЧИТЬ ПО
	|	Тег";
	
	Выборка = Запрос.Выполнить().Выбрать();	
	Выборка.Следующий();
	НазваниеЦвета = Выборка.НазваниеЦвета;
	Цвет = Новый Цвет (Выборка.Красный, Выборка.Зеленый, Выборка.Синий);
	
	ДанныеЦвета = Новый Структура("НазваниеЦвета, Цвет", НазваниеЦвета, Цвет);
	
	Возврат ДанныеЦвета;
	
КонецФункции

&НаСервере
Функция ПолучениеЦвета(НазваниеЦвета)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭП_КлассификаторЦветов.Красный КАК Красный,
	|	ЭП_КлассификаторЦветов.Зеленый КАК Зеленый,
	|	ЭП_КлассификаторЦветов.Синий КАК Синий
	|ИЗ
	|	Справочник.ЭП_КлассификаторЦветов КАК ЭП_КлассификаторЦветов
	|ГДЕ
	|	ЭП_КлассификаторЦветов.Ссылка = &НазваниеЦвета";
	
	Запрос.УстановитьПараметр("НазваниеЦвета", НазваниеЦвета);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Цвет = Новый Структура("Красный, Зеленый, Синий", Выборка.Красный, Выборка.Зеленый, Выборка.Синий);
	
	Возврат Цвет;
	
КонецФункции

#КонецОбласти



