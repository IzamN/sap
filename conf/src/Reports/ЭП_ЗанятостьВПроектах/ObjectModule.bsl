
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; 
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки(); 
	
	Отборы = НастройкиОтчета.Отбор.Элементы;
	ЭлементыПользовательскихНастроек = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
	ДиаграммаПроекты = НайтиЭлементНастроек(НастройкиОтчета.Структура, "Имя", "ДиаграммаПроекты");
	ДиаграммаСотрудники = НайтиЭлементНастроек(НастройкиОтчета.Структура, "Имя", "ДиаграммаСотрудники");
	
	ПолеПроект = Новый ПолеКомпоновкиДанных("Проект");
	ПолеСотрудник = Новый ПолеКомпоновкиДанных("Сотрудник");
	
	ИдентификаторПроект = НайтиИдентификаторОтбора(Отборы,ПолеПроект);
	ИдентификаторСотрудник = НайтиИдентификаторОтбора(Отборы,ПолеСотрудник);
	
	Если ИдентификаторПроект = Неопределено ИЛИ ИдентификаторСотрудник = Неопределено Тогда
		СтандартнаяОбработка = Истина;
		Возврат;
	КонецЕсли;
	
	ПользовательскиеНастройкиПроект = ЭлементыПользовательскихНастроек.Найти(ИдентификаторПроект);
	ПользовательскиеНастройкиСотрудник = ЭлементыПользовательскихНастроек.Найти(ИдентификаторСотрудник);	
	
	Если ПользовательскиеНастройкиПроект.Использование Тогда
		ДиаграммаПроекты.Использование = Ложь;
		ДиаграммаСотрудники.Использование = Истина;
	КонецЕсли;
	
	Если НЕ ПользовательскиеНастройкиПроект.Использование Тогда
		ДиаграммаПроекты.Использование = Истина;
		ДиаграммаСотрудники.Использование = Ложь;
	КонецЕсли;
	
	Если ПользовательскиеНастройкиПроект.Использование 
			И ПользовательскиеНастройкиСотрудник.Использование  Тогда
		ДиаграммаПроекты.Использование = Ложь;
		ДиаграммаСотрудники.Использование = Ложь;
	КонецЕсли;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);  
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);	
	
КонецПроцедуры   

Функция НайтиЭлементНастроек(СтруктураОтчета, КолонкаПоиска, ЗначениеПоиска)
	
	Для Каждого ЭлементСтруктуры Из СтруктураОтчета Цикл 
		
		Если ЭлементСтруктуры[КолонкаПоиска] = ЗначениеПоиска Тогда
			Возврат ЭлементСтруктуры;	
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция НайтиИдентификаторОтбора(ЭлементыОтбора, ЗначениеПоиска)
	
	Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл 
		
		Если ЭлементОтбора.ЛевоеЗначение = ЗначениеПоиска Тогда
			Возврат ЭлементОтбора.ИдентификаторПользовательскойНастройки;	
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции


