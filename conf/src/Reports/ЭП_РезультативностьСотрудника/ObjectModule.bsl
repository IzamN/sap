
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	УстановитьПривилегированныйРежим(Истина);
	ТекПользователь = Пользователи.ТекущийПользователь();
	ТекСотрудник = ТекПользователь.ЭП_Сотрудник;
	
	ТекПараметр = Новый ПараметрКомпоновкиДанных("Сотрудник");
	ЗначениеПараметра = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ТекПараметр);
	ЗначениеПараметра.Значение = ТекСотрудник;
	ЗначениеПараметра.Использование = Истина;
	
	// Получить результирующие настройки отчета
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	// Получить период оценивания
	//ТекПараметр = Новый ПараметрКомпоновкиДанных("ПериодГод");
	//ЗначениеПараметра = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(ТекПараметр);
	//ПериодГод = ЗначениеПараметра.Значение;
	//
	//ТекПараметр = Новый ПараметрКомпоновкиДанных("ПериодМесяц");
	//ЗначениеПараметра = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(ТекПараметр);
	//ПериодМесяц = ЗначениеПараметра.Значение;
	
	ПериодОценивания = ДобавитьМесяц(НачалоМесяца(ТекущаяДата()),-1);
	
	// Получить результат расчета по сотруднику
	ТаблицаДанных = ПолучитьТаблицуДанных(ПериодОценивания, ТекСотрудник);
	ТаблицаДетализации = ПолучитьТаблицуДетализации(ПериодОценивания, ТекСотрудник); 
	
	ИтоговыйПоказатель = 0;
	Для каждого СтрокаТЧ Из ТаблицаДетализации Цикл
	
		ИтоговыйПоказатель = ИтоговыйПоказатель + СтрокаТЧ.ЗначениеДетализации * СтрокаТЧ.КоэффициентЗагрузки;	
	
	КонецЦикла;
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("ИтоговыйПоказатель", Окр(ИтоговыйПоказатель,2));
	// Вывод отчета
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТаблицаДанных",ТаблицаДанных);                    
	ВнешниеНаборыДанных.Вставить("ТаблицаДетализации", ТаблицаДетализации);
	
	//Макет компоновки 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	//Компоновка данных 
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	//Вывод результата 
	ДокументРезультат.Очистить();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);	
    УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

Функция ПолучитьТаблицуДанных(ПериодОценивания, ТекСотрудник)
	
    Отбор = Новый Структура("Сотрудник", ТекСотрудник);
	Результат = Отчеты.ЭП_РасчетРезультативности.ПолучитьРасчетРезультативности(ПериодОценивания, Отбор);
	
	// Преобразовать в таблицу значений	
	ТаблицаДанных = Новый ТаблицаЗначений;
	Для Каждого КолонкаДерева Из Результат.Колонки Цикл
		ТаблицаДанных.Колонки.Добавить(КолонкаДерева.Имя, КолонкаДерева.ТипЗначения);
	КонецЦикла;
	ТаблицаДанных.Колонки.Добавить("КоличествоОценок", ОбщегоНазначения.ОписаниеТипаЧисло(10,0));
	
	// Уровни дерева: Профиль расчета, Сотрудник, Показатель. Нужен только показатель
	Для Каждого Строка1 Из Результат.Строки Цикл // Профиль расчета
		Для Каждого Строка2 Из Строка1.Строки Цикл // Сотрудник
			Для Каждого Строка3 Из Строка2.Строки Цикл // Показатель
				
				ПромежуточныйКомментарий = "";
				КоличествоОценок = 0;
				ПризнакПустыхКомментариев = Ложь;
				Для Каждого Строка4 Из Строка3.Строки Цикл
					Если ЗначениеЗаполнено(Строка4.Ответственный) Тогда
						КоличествоОценок = КоличествоОценок+1;
					КонецЕсли;
					Если ЗначениеЗаполнено(Строка4.Комментарий) Тогда
						 
						ПромежуточныйКомментарий = ПромежуточныйКомментарий + Символы.ПС + Строка4.Комментарий;						
						ПризнакПустыхКомментариев = Истина;						
					КонецЕсли;                                    
					
				КонецЦикла;  
				НоваяСтрока = ТаблицаДанных.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка3);
				Если ПризнакПустыхКомментариев Тогда
				
					НоваяСтрока.Комментарий = ПромежуточныйКомментарий;					
				
				КонецЕсли;
				НоваяСтрока.КоличествоОценок = КоличествоОценок; 
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТаблицаДанных;
	
КонецФункции

Функция ПолучитьТаблицуДетализации(ПериодОценивания, ТекСотрудник)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЭП_РасчетПоказателейИтогиПЗК.Проект КАК ПроектДетализация,
	               |	ЭП_РасчетПоказателейИтогиПЗК.Значение КАК ЗначениеДетализации,
	               |	ЭП_РасчетПоказателейИтогиПЗК.КоэффициентЗагрузки КАК КоэффициентЗагрузки
	               |ИЗ
	               |	Документ.ЭП_РасчетПоказателей.ИтогиПЗК КАК ЭП_РасчетПоказателейИтогиПЗК
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭП_РасчетПоказателей КАК ЭП_РасчетПоказателей
	               |		ПО ЭП_РасчетПоказателейИтогиПЗК.Ссылка = ЭП_РасчетПоказателей.Ссылка
	               |ГДЕ
	               |	НАЧАЛОПЕРИОДА(ЭП_РасчетПоказателей.Дата, МЕСЯЦ) = &ПериодОценивания
	               |	И ЭП_РасчетПоказателейИтогиПЗК.Сотрудник = &ТекСотрудник
	               |	И НЕ ЭП_РасчетПоказателей.ПометкаУдаления
	               |	И ЭП_РасчетПоказателей.Проведен";
	Запрос.УстановитьПараметр("ПериодОценивания", ПериодОценивания);
	Запрос.УстановитьПараметр("ТекСотрудник", ТекСотрудник); 
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();

	Возврат ТаблицаДанных;
	
КонецФункции

